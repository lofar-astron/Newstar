C++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
C.Ident:	CPL_OBJ
C.Keywords:	Compiler Utility, Object File
C.Author:	Friso Olnon (NFRA, Dwingeloo)
C.Language:	DWARF-Fortran
C.Environment:	VAX or Alliant
C.Comments:
C	Common variables used:
C
C	INTEGER*4	CPL$OBJLUN	! (m) lun for file (-1 if not open)
C	CHARACTER*64	CPL$OBJNAME	! (m) full name of last opened file
C	INTEGER*4	CPL$OBJLREC	! (m) record length in bytes
C	INTEGER*4	CPL$OBJNREC	! (m) nr of records written
C
C	NAME, LREC and NREC are initially: ' ', 0, 0.
C	NAME and LREC are filled by OPEN and only reset by DELETE.
C	NREC is (re)set to 0 by OPEN and DELETE, and incremented by WRITE.
C	So, the info is still available if the file is only closed.
C
C	NOTE: for open keyed-access object files (CPL$OBJNREC = -1)
C	calls to CPL_OBJ_WRITE are passed to CPL_KOBJ_WRITE.
C
C.Version:	890205 FMO - Creation
C.Version:	900403 FMO - removed keyed-access branch
C			- expand file name using FILNAM_FULL
C.Version:	900502 FMO - new GEN_LUN module
C.Version:	920214 GvD - no optional arguments in MSG anymore
C			- use GEN_FILCRE to open the output file
C.Version:	920423 GvD - use GEN_FILOPN to open the input file
C.Version:	940119 CMV - included OPEN statement in this file
C--------------------------------------------------------------------------
C++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
	INTEGER*4 FUNCTION CPL_OBJ_OPEN (FILESPEC,RECLEN)
C
	INCLUDE 'WNG_DEF'
	INCLUDE 'DWC_DEF'
	INCLUDE 'CPL_2_DEF'
C
	CHARACTER*(*)	FILESPEC	! (i) file specification
	INTEGER*4	RECLEN		! (i) record length in bytes
C
C.Purpose:	Open a new object file (direct access)
C.Returns:	Status code (.TRUE. for success, .FALSE. otherwise)
C	success	CPL_SUCCESS
C	fatal	CPL_OBJOPNERR	error report left in message buffer
C.Notes:
C--------------------------------------------------------------------------
C
C
	INTEGER		FILNAM_FULL, MSG_SET, GEN_FORIOS
C
	INTEGER*4	IS, LN, RCL
C
C
	IF (CPL$OBJLUN.GE.0) GOTO 999
C
	IS = FILNAM_FULL (FILESPEC,CPL$OBJNAME,LN,' ')
	CALL WNGLUN(CPL$OBJLUN)
	IF (CPL$OBJLUN.EQ.0) GOTO 999
C
#ifdef wn_hp__
	RCL = RECLEN
#else
  #ifdef wn_li__
	RCL = RECLEN
  #else
	RCL = RECLEN/4
  #endif
#endif
	OPEN (UNIT=CPL$OBJLUN,FILE=CPL$OBJNAME(:LN),
#ifdef wn_li__
	1	FORM='UNFORMATTED',
#endif
	1	STATUS='UNKNOWN',ERR=992,ACCESS='DIRECT',RECL=RCL)
	CPL$OBJLREC = RECLEN
	CPL$OBJNREC = 0
C
	CPL_OBJ_OPEN = CPL_SUCCESS
	RETURN
C
 992	IS = GEN_FORIOS(CPL$OBJNAME(:LN))
	CALL WNGLUF(CPL$OBJLUN)
	CPL$OBJLUN = -1
C
 999	CPL_OBJ_OPEN = MSG_SET (CPL_OBJOPNERR,0)
	RETURN
	END
C++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
	INTEGER*4 FUNCTION CPL_OBJ_CLOSE ()
C
	INCLUDE 'WNG_DEF'
	INCLUDE 'DWC_DEF'
	INCLUDE 'CPL_2_DEF'
C
C
C.Purpose:	Close the object file
C.Returns:	Status code (.TRUE. for success, .FALSE. otherwise)
C	success	CPL_SUCCESS	also if the file is not open
C	error	CPL_OBJCLOERR	error report left in message buffer
C.Notes:
C--------------------------------------------------------------------------
C
	INTEGER		GEN_FORIOS, MSG_SET
C
	INTEGER*4	IS
C
C
	IF (CPL$OBJLUN.GE.0) THEN
		CLOSE (UNIT=CPL$OBJLUN,ERR=999)
		CALL WNGLUF(CPL$OBJLUN)
		CPL$OBJLUN = -1
	ENDIF
C
	CPL_OBJ_CLOSE = CPL_SUCCESS
	RETURN
C
 999	IS = GEN_FORIOS (CPL$OBJNAME)
	CPL_OBJ_CLOSE = MSG_SET (CPL_OBJCLOERR,0)
	RETURN
	END
C++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
	INTEGER*4 FUNCTION CPL_OBJ_DELETE ()
C
	INCLUDE 'WNG_DEF'
	INCLUDE 'DWC_DEF'
	INCLUDE 'CPL_2_DEF'
C
C
C.Purpose:	Delete the object file last opened
C.Returns:	Status code (.TRUE. for success, .FALSE. otherwise)
C	success	CPL_SUCCESS
C	error	CPL_OBJDELERR	error report left in message buffer
C.Notes:
C	If the file is not open, it will be opened first.
C--------------------------------------------------------------------------
C
	INTEGER*4	GEN_FORIOS, MSG_SET
C
	INTEGER*4	IS,RCL
C
C
	IF (CPL$OBJLUN.LT.0) THEN
		CALL WNGLUN(CPL$OBJLUN)
		IF (CPL$OBJLUN.EQ.0) GOTO 999
#ifdef wn_hp__
		RCL = CPL$OBJLREC
#else
  #ifdef wn_li__
		RCL = CPL$OBJLREC
  #else
		RCL = CPL$OBJLREC/4
  #endif
#endif
		OPEN (UNIT=CPL$OBJLUN,FILE=CPL$OBJNAME,
#ifdef wn_li__
	1	FORM='UNFORMATTED',
#endif
	1	  STATUS='UNKNOWN',ERR=992,ACCESS='DIRECT',RECL=RCL)
		IF (IAND(IS,1).EQ.0) GOTO 992
	ENDIF
C
	CLOSE (UNIT=CPL$OBJLUN,DISPOSE='DELETE',ERR=999)
 992	CALL WNGLUF(CPL$OBJLUN)
C
	CPL$OBJLUN = -1
	CPL$OBJNAME = ' '
	CPL$OBJLREC = 0
	CPL$OBJNREC = 0
C
	CPL_OBJ_DELETE = CPL_SUCCESS
	RETURN
C
 999	IS = GEN_FORIOS (CPL$OBJNAME)
	CPL_OBJ_DELETE = MSG_SET(CPL_OBJDELERR,0)
	RETURN
	END
C++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
	INTEGER*4 FUNCTION CPL_OBJ_INQUIRE (FULLSPEC,LSPEC)
C
	INCLUDE 'WNG_DEF'
	INCLUDE 'DWC_DEF'
	INCLUDE 'CPL_2_DEF'
C
	CHARACTER*(*)	FULLSPEC	! (o) full file specification
	INTEGER*4	LSPEC		! (o) its significant length
C
C.Purpose:	Get the full spec of the object file last opened
C.Returns:	Status code (.TRUE. for success, .FALSE. otherwise)
C	success	CPL_SUCCESS
C.Notes:
C--------------------------------------------------------------------------
C
	INTEGER WNCALN
C
C
	FULLSPEC = CPL$OBJNAME
	LSPEC = WNCALN(FULLSPEC)
C
	CPL_OBJ_INQUIRE = CPL_SUCCESS
	RETURN
	END
C+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
	INTEGER*4 FUNCTION CPL_OBJ_WRITE (BARR,LARR)
C
	INCLUDE 'WNG_DEF'
	INCLUDE 'DWC_DEF'
	INCLUDE 'CPL_2_DEF'
C
	BYTE		BARR(*)		! (i) byte array to be written
	INTEGER*4	LARR		! (i) significant length of byte array
C
C.Purpose:	Write next object record(s)
C.Returns:	Status code (.TRUE. for success, .FALSE. otherwise)
C	success	CPL_SUCCESS
C	fatal	CPL_OBJWRTERR	error report left in message buffer
C.Notes:
C-------------------------------------------------------------------------
C
	INTEGER		MSG_SET, GEN_FORIOS
C
	INTEGER*4	IS, NDONE, NWRITE
C
C
	IF (CPL$OBJLUN.LT.0) THEN
		CALL WNCTXT(F_TP,'Object file is not open')
		GOTO 999
	ENDIF
C
	NDONE = 0
	DO WHILE (NDONE.LT.LARR)
		NWRITE = MIN (CPL$OBJLREC,LARR-NDONE)
		CPL$OBJNREC = CPL$OBJNREC+1
		WRITE (CPL$OBJLUN,REC=CPL$OBJNREC,ERR=998)
	1				(BARR(I),I=NDONE+1,NDONE+NWRITE)
		NDONE = NDONE+NWRITE
	ENDDO
C
C					Return
C
	CPL_OBJ_WRITE = CPL_SUCCESS
	RETURN
C
 998	IS = GEN_FORIOS (CPL$OBJNAME)
 999	CPL_OBJ_WRITE = MSG_SET (CPL_OBJWRTERR,1)
	CALL WNCTXT(DWLOG,DWMSG,CPL$OBJNREC)
	RETURN
	END

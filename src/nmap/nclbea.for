C+ NCLBEA.FOR
C  WNB 910809
C
C  Revisions:
C	WNB 920403	Typo in model epoch setting
C	WNB 921202	Cater for J2000
C	WNB 930928	Add instrument
C	CMV 931117	Corrected instrument: MPH_INST_J should be _1
C
	SUBROUTINE NCLBEA(MPH,APH)
C
C  Clean for BEAM option
C
C  Result:
C	CALL NCLBEA( MPH_B(0:*):I, APH_B(0:*):I)
C				Cleans a map according to BEAM option.
C				MPH and APH are the map and antenna pattern
C				headers.
C
C
C  Include files:
C
	INCLUDE 'WNG_DEF'
	INCLUDE 'MPH_O_DEF'		!MAP HEADER
	INCLUDE 'MDH_O_DEF'		!MODEL HEADER
	INCLUDE 'NCL_DEF'
C
C  Parameters:
C
C
C  Arguments:
C
	BYTE MPH(0:*)			!MAP HEADER
	BYTE APH(0:*)			!AP HEADER
C
C  Function references:
C
	LOGICAL WNGGVA			!GET MEMORY
	INTEGER*2 WNGGI			!GET I VALUE
	REAL WNGGE			!GET E VALUE
	LOGICAL NMOSLG			!GET SOURCE SPACE
C
C  Data declarations:
C
	INTEGER BEMADR,MPADR		!BUFFER ADDRESSES
	INTEGER RG(0:1)			!SOURCE RANGE
	  DATA RG/1,10000000/
	INTEGER MDHJ(0:MDHHDL/4-1)	!MODEL HEADER
	  REAL MDHE(0:MDHHDL/4-1)
	  DOUBLE PRECISION MDHD(0:MDHHDL/8-1)
	  EQUIVALENCE (MDHJ,MDHE,MDHD)
C-
C
C GET BUFFERS
C
	J=TAREA(2,0)				!# OF LINES
	JS=WNGGVA(LB_E*(2*J+1)*(TAREA(3,0)+1),BEMADR) !BEAM AREA
	IF (JS) JS=WNGGVA(LB_E*J*TAREA(3,0),MPADR) !MAP AREA
	IF (.NOT.JS) THEN
	  CALL WNCTXT(F_TP,'Cannot obtain map/beam buffers')
	  CALL WNGEX				!STOP
	END IF
C
C READ DATA INTO BUFFERS
C
	CALL NCLBRD(TAREA(2,0),TAREA(3,0),
	1		A_B(BEMADR-A_OB),A_B(MPADR-A_OB),MPH,APH)
C
C INIT SOURCE LIST
C
	CALL NMOHMF(7,MDHJ)			!GET HEADER 7
	CALL NMOHZD(MDHJ)			!CLEAR SOURCES
	CALL WNGMV(LB_D,MPH(MPH_RA_1),MDHD(MDH_RA_D)) !SET RA
	CALL WNGMV(LB_D,MPH(MPH_DEC_1),MDHD(MDH_DEC_D)) !SET DEC
	CALL WNGMV(LB_D,MPH(MPH_FRQ_1),MDHD(MDH_FRQ_D)) !SET FREQ.
	IF (WNGGI(MPH(MPH_EPT_1)).EQ.1) THEN	!1950/2000
	  MDHJ(MDH_TYP_J)=2
	  MDHE(MDH_EPOCH_E)=WNGGE(MPH(MPH_EPO_1))
	ELSE					!APPARENT
	  MDHJ(MDH_TYP_J)=1
	  MDHE(MDH_EPOCH_E)=0
	END IF
	CALL WNGMV(LB_J,MPH(MPH_INST_1),MDHJ(MDH_BITS_J)) !SET INSTRUMENT
	IF (.NOT.NMOSLG(SRCLIM,MDHJ)) THEN	!GET AREA
	  CALL WNCTXT(F_TP,'No space for clean source list')
	  CALL WNGEX				!STOP
	END IF
C
C DO ACTUAL CLEAN
C
	DO WHILE (CURMAX.GT.ABS(CLLIM*WNGGE(MPH(MPH_MAX_1))) .AND.
	1		MDHJ(MDH_NSRC_J).LT.SRCLIM)
	  CALL NCLBCL(TAREA(2,0),TAREA(3,0),
	1		A_B(BEMADR-A_OB),A_B(MPADR-A_OB),MPH,MDHJ) !CLEAN
	END DO
C
C FINISH
C
	CALL NMOHMT(MDHJ,7)			!SET HEADER DATA
	CALL NMORDM(7,-1)			!ADD SOURCES TO GENERAL HEADER
	CALL NMOAMG				!MERGE COMPONENETS
	CALL NMOPTT(F_TP,RG)			!SHOW TOTAL FLUX
	IF (FCAAP.EQ.0) THEN			!NO MODEL FILE GIVEN
	  CALL NMODAX(J)			!LET USER ACT
	ELSE
	  CALL NMOWRI(FCAAP,-1)			!WRITE SOURCE MODEL
	END IF
C
C WRITE RESIDUAL
C
	IF (RESMDL) THEN			!RESIDUAL MAP WANTED
	  CALL NCLBWR(TAREA(2,0),TAREA(3,0),A_B(MPADR-A_OB),MPH) !WRITE RESID.
	END IF
C
C CLEAR BUFFERS
C
	J=2*TAREA(2,0)+1
	CALL WNGFVA(LB_E*J*(TAREA(3,0)/2+1),BEMADR)
	J=TAREA(2,0)
	CALL WNGFVA(LB_E*J*TAREA(3,0),MPADR)
C
C
	RETURN
C
	END

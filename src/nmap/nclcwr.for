C+ NCLCWR.FOR
C  WNB 920106
C
C  Revisions:
C	WNB 920131	Logic error in pointer value
C
	SUBROUTINE NCLCWR(FCAR,IMPH,MPHP,SGNR)
C
C  Write initial residual map
C
C  Result:
C	CALL NCLCWR( FCAR_J:I, IMPH_B(0:*):I, MPHP_J:O, SGNR_J(0:7):O)
C				Write residual map by copying the map
C				header IMPH, and returning header pointer
C				MPHP on file FCAR, and possible set name
C				in SGNR
C
C  Include files:
C
	INCLUDE 'WNG_DEF'
	INCLUDE 'GFH_O_DEF'		!GENERAL FILE HEADER
	INCLUDE 'SGH_O_DEF'		!SUB-GROUP HEADER
	INCLUDE 'MPH_O_DEF'		!MAP HEADER
	INCLUDE 'NCL_DEF'	
C
C  Parameters:
C
C
C  Arguments:
C
	INTEGER FCAR			!RESIDUAL FILE
	BYTE IMPH(0:MPHHDL-1)		!MAP HEADER
	INTEGER MPHP			!POINTER TO OUTPUT MAP
	INTEGER SGNR(0:7)		!SET NAME
C
C  Function references:
C
	LOGICAL WNFRD			!READ DISK
	LOGICAL WNFWR			!WRITE DISK
	INTEGER WNFEOF			!FILE POINTER
	LOGICAL WNDLNF,WNDLNG,WNDLNK	!FIND/MAKE SUB-GROUPS
	INTEGER WNGGJ			!GET VALUE
	CHARACTER*32 WNTTSG		!SET NAME
C
C  Data declarations:
C
	CHARACTER*32 TXT
	INTEGER SGPH(0:7)		!NAME FINDER
	BYTE MPH(0:MPHHDL-1)		!RESIDUAL MAP HEADER
	  INTEGER MPHJ(0:MPHHDL/4-1)
	  REAL MPHE(0:MPHHDL/4-1)
	  EQUIVALENCE (MPH,MPHJ,MPHE)
C-
C
C INITIALISE
C
	CALL WNGMV(MPHHDL,IMPH,MPH)		!INIT. RESIDUAL MAP HEADER
	IF (FCAR.EQ.FCAMAP) THEN		!RESIDUAL WANTED
	  MPHP=WNFEOF(FCAR)			!HEADER POINTER
	ELSE
	  MPHP=MPHHDL
	END IF
	IF (.NOT.WNFWR(FCAR,MPHHDL,MPH,MPHP)) THEN !WRITE RESIDUAL HEADER
 10	  CONTINUE
	  CALL WNCTXT(F_TP,'Initiation error residual map')
	  CALL WNGEX				!STOP
	END IF
	IF (FCAR.EQ.FCAMAP) THEN		!INIT. MAP SET FOR RESIDUAL
	  IF (.NOT.WNDLNF(GFH_LINKG_1,MAPNAM(0),SGH_GROUPN_1,FCAR,SGPH(0),
	1		SGNR(0))) THEN		!FIND CORRECT SET
 20	    CONTINUE
	    CALL WNCTXT(F_TP,'Cannot create residual map linkage')
	    CALL WNGEX				!STOP PROGRAM
	  END IF
	  DO I=1,4				!FIND ALL LEVELS
	    IF (.NOT.WNDLNF(SGPH(I-1)+SGH_LINKG_1,MAPNAM(I),SGH_GROUPN_1,
	1		FCAR,SGPH(I),SGNR(I))) GOTO 20
	  END DO
	  IF (.NOT.WNDLNK(GFH_LINK_1,MPHP,MPH_SETN_1,FCAR)) GOTO 20 !LINK MAP
	  IF (.NOT.WNDLNG(SGPH(4)+SGH_LINKG_1,MPHP,SGH_GROUPN_1,
	1		FCAR,SGPH(5),SGNR(5))) GOTO 20 !LINK SUB-GROUP
	  SGNR(6)=-1				!CLOSE OFF NAME
	END IF
	IF (.NOT.WNFRD(FCAR,MPHHDL,MPH,MPHP)) GOTO 10 !REREAD RESIDUAL HEADER
C
C FILL HEADER DATA
C
	IF (FCAR.EQ.FCAMAP) THEN		!RESIDUAL WANTED SAVED
	  MPHJ(MPH_MDP_J)=WNFEOF(FCAR)		!SET DATA POINTER
	ELSE
	  MPHJ(MPH_MDP_J)=MPHHDL+MPHHDL		!SET DATA POINTER
	END IF
	CALL WNCTXS(TXT,'Residual !AS',WNTTSG(MAPNAM,0)) !SET TEXT
	CALL WNGMFS(MPH_UCM_N,TXT,MPH(MPH_UCM_1))
	IF (.NOT.WNFWR(FCAR,MPHHDL,MPH,MPHP)) GOTO 10 !REWRITE HEADER
C
	RETURN
C
C
	END

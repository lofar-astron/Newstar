C+ NATRRT.FOR
C  WNB 920429
C
C  Revisions:
C     RPN 29/9/88
C     HM  11/5/90  Made mods necessary for compilation on SUNs
C     HM  29/1/91  Reduced lines to 72 chars
C
	SUBROUTINE NATRRT(FCA,FCAPT,M,II,ENDHDR)
C
C  Read RPFITS tape table. Based on RPFITS_READ_TABLE
C
C  Result:
C
C	CALL NATRRT( FCA_J:I, FCAPT_J:IO, M_C80(32):IO, II_J:I,
C					ENDHDR_L:IO)
C				Read a table from FCA based on card images in M
C				starting at II currently (-1 indicates only FG 
C				wanted). ENDHDR will be set .true. if
C				end-of-header encountered.
C
C  Include files:
C
	INCLUDE 'WNG_DEF'
	INCLUDE 'RPF_DEF'	!RPFITS OUTPUT
C
C  Parameters:
C
C
C  Arguments:
C
	INTEGER FCA		!FILE CONTROL AREA
	INTEGER FCAPT		!FILE POINTER
	CHARACTER*80 M(32)	!CARD IMAGES
	INTEGER II		!CURRENT CARD IMAGE (OR -1 FOR FG)
	LOGICAL ENDHDR		!END-OF-HEADER SEEN
C
C  Entry points:
C
C
C  Function references:
C
	LOGICAL WNFRD		!READ DISK
C
C  Data declarations:
C
	LOGICAL FG_ONLY		!ONLY FG
	INTEGER ICHR(20,32)	!BUFFER
C-
	I=ABS(II)
	FG_ONLY=(II.EQ.-1)
	DO WHILE (.NOT.ENDHDR)
	  IF (NCARD.LT.0) THEN
	    CARD(-NCARD)=M(I)
	    NCARD=NCARD-1
	  END IF
	  IF (M(I)(1:8).EQ.'TABLE IF') THEN
	    CALL NATRIF(FCA,FCAPT,M,I)
	  ELSE IF (M(I)(1:8).EQ.'TABLE SU') THEN
	    CALL NATRSU(FCA,FCAPT,M,I)
	  ELSE IF (M(I)(1:8).EQ.'TABLE FG') THEN
	    CALL NATRFG(FCA,FCAPT,M,I)
	  ELSE IF (M(I)(1:8).EQ.'TABLE AN') THEN
	    CALL NATRAN(FCA,FCAPT,M,I)
	  ELSE IF (M(I)(1:8).EQ.'TABLE MT') THEN
	    CALL NATRMT(FCA,FCAPT,M,I)
	  ELSE IF (M(I)(1:8).EQ.'TABLE CU') THEN
	    CALL NATRCU(FCA,FCAPT,M,I)
	  ELSE IF (M(I)(1:8).EQ.'END     ') THEN
	    ENDHDR=.TRUE.
C
	    RETURN
	  END IF
	  IF (FG_ONLY) THEN
	    ENDHDR=.FALSE.
C
	    RETURN
	  END IF
	  I=I+1
	  IF (I.GT.32) THEN
	    IF (.NOT.WNFRD(FCA,2560,ICHR,FCAPT)) THEN	!READ BLOCK
	      ENDHDR=.TRUE.				!ASSUME END IF READ
C							!ERROR
	      RETURN
	    END IF
	    FCAPT=FCAPT+2560				!UPDATE POINTER
	    DO I=1,32
	      CALL WNGMTS(80,ICHR(1,I),M(I))		!SAVE IN STRING
	    END DO
	    I=0
	  END IF
	END DO
C
	RETURN
C
C
	END

C+ NCARGS.FOR
C  WNB 900312
C
C  Revisions:
C	WNB 910812	Add ALIGN
C	WNB 910930	Narrower check
C	WNB 911024	Running noise
C	WNB 911025	Zero division check
C	WNB 930826	New model data
C       WNB 950613	New LSQ routines
C	WNB 980701	Add for new MIFR calculations
C	CMV 030116	Acommodated for unsorted IFR table
C
	LOGICAL FUNCTION NCARGS(MAR,NIFR,IFR,BASEL,NDEG,
	1			IRED,WGT,AWGT,CDAT,AMP,PHAS,CMOD,CWGT,
	1			CSOL,SOL,MU,ME)
C
C  Calculate redundancy gain solution
C
C  Result:
C
C	NCARGS_L = NCARGS( MAR_J:I, NIFR_J:I, 
C				IFR_I(0:*):I, BASEL_E(0:*):I, NDEG_J:IO,
C				IRED_J(0:NIFR-1):I, WGT_E(0:*,0:1):I,
C				AWGT_E(0:*,0:1):I, CDAT_X(0:*,0:1):I,
C				AMP_E(0:*,0:1):I, PHAS_E(0:*,0:1):I,
C				CMOD_X(0:*,0:3):I, CWGT_E(0:*):I,
C				CSOL_E(0:*,0:1,0:1):I,SOL_E(0:*,0:1,0:1):O,
C				MU_E:O, ME_E:O)
C					Calculate the redundancy gain solution
C					in SOL, with adjustment error MU and
C					mean errors ME using CSOL as approximate
C					solution.
C					WGT is the weight, AWGT the amplitude
C					eighted weight, CDAT/AMP/PHAS
C					the data. IRED specifies the
C					redundant baselines.
C					CMOD is the model with sqrt(weights)
C					CWGT.
C					MAR is the solution area for the
C					telescopes, using NIFR interferometers
C					and a degeneracy of NDEG.
C					IFR are the interferometer
C					specifications.
C	NCASGS_L = NCASGS( ...)
C					Use model for constraints (selfcal)
C	NCAAGS_L = NCAAGS( ..., NUK_J:I, ALEQ_E(0:*,0:*):I)
C					Use model to align NUK parameters using
C					equations ALEQ
C	NCARG1_L = NCARG1( ...)
C					Calculate X and Y simultaneous
C	NCARG2_L = NCARG2( ...)
C					Calculate X and Y simultaneous with Q=0
C	NCARGE_L = NCARGE( MAR_J:I, NIFR_J:I, 
C				IFR_I(0:*):I, BASEL_E(0:*):I, NDEG_J:IO,
C				IRED_J(0:NIFR-1):I, WGT_E(0:*,0:1):I,
C				AWGT_E(0:*,0:1):I, CDAT_X(0:*,0:1):I,
C				AMP_E(0:*,0:1):I, PHAS_E(0:*,0:1):I,
C				CMOD_X(0:*,0:3):I, CWGT_E(0:*):I,
C				CSOL_E(0:*,0:1,0:1):I,SOL_E(0:*,0:1,0:1):I,
C				MU_E:I, ME_E:I, ARMS_E(0:2):I,
C				JAV_J(0:*,0:*,0:1):IO, EAV_E(0:*,0:*,0:1):IO,
C				DAV_D(0:*,0:*,0:1):IO)
C					Calculate all errors in the average
C					arrays JAV, EAV and DAV.
C					ARMS is the average amplitude of scan
C	NCARGC_L = NCARGC( ...)
C					Correct errors back
C	NCASGE_L = NCASGE( ...)
C					Calculate selfcal errors
C	NCASGC_L = NCASGC( ...)
C					Correct selfcal errors back
C	NCAAGE_L = NCAAGE( ...)
C					Calculate align errors
C	NCAAGC_L = NCAAGC( ...)
C					Correct align errors back
C
C				JAV, EAV, DAV contain:
C					*,*,0		gain
C					*,*,1		phase
C					0,0		noise per scan
C					1,0		inconsistency per scan
C					2,0		total noise
C					3,0		overall running noise
C					4,0		max. deviation in scan
C					5,0		total average noise
C					6,0		total average incons.
C					7,0		total average ampl.
C					*,1		inconsistency per ifr
C					*,2		average rms per ifr
C					*,3		gain per telescope
C!980701				*,4		weighted incons per ifr
C
C  Include files:
C
	INCLUDE 'WNG_DEF'
	INCLUDE 'LSQ_O_DEF'
	INCLUDE 'STH_O_DEF'			!SET HEADER
C
C  Entry points:
C
	LOGICAL NCARG1,NCARG2			!CALCULATE X/Y SIMULTANEOUS
	LOGICAL NCASGS				!CALCULATE SELFCAL
	LOGICAL NCAAGS				!CALCULATE ALIGN
	LOGICAL NCARGE,NCARGC			!CALCULATE ERRORS
	LOGICAL NCASGE,NCASGC			!CALCULATE SELFCAL ERRORS
	LOGICAL NCAAGE,NCAAGC			!CALCULATE ALIGN ERRORS
C
C  Parameters:
C
C
C  Arguments:
C
	INTEGER MAR				!SOLUTION AREA POINTER
	INTEGER NIFR				!TOTAL # OF INTERFEROMETERS
	INTEGER*2 IFR(0:*)			!INTERFEROMETER TELESCOPES
	INTEGER NDEG				!DEGENERACY LEVEL
	REAL BASEL(0:*)				!BASELINES
	INTEGER IRED(0:*)			!REDUNDANCY INDICATOR
	REAL WGT(0:STHIFR-1,0:*)		!DATA WEIGHT X,Y
	REAL AWGT(0:STHIFR-1,0:*)		!AMPLITUDE WEIGHTED WEIGHT X,Y
	COMPLEX CDAT(0:STHIFR-1,0:*)		!DATA COMPLEX X,Y
	REAL AMP(0:STHIFR-1,0:*)		!DATA AMPLITUDE X,Y
	REAL PHAS(0:STHIFR-1,0:*)		!DATA PHASE X,Y
	COMPLEX CMOD(0:STHIFR-1,0:*)		!MODEL COMPLEX XYX
	REAL CWGT(0:*)				!MODEL WEIGHT**0.5
	REAL SOL(0:STHTEL-1,0:1,0:1)		!SOLUTION X,Y G,P
	REAL CSOL(0:STHTEL-1,0:1,0:1)		!CONTINUITY SOLUTION G,P X,Y
	REAL MU					!ADJUSTMENT ERROR
	REAL ME					!MEAN ERRORS SOLUTION
	INTEGER NUK				!# OF ALIGN PARAMETERS
	REAL ALEQ(0:STHTEL-1,0:*)		!ALIGN EQUATIONS
	REAL ARMS(0:2)				!AVERAGE AMPL.
	INTEGER JAV(0:STHIFR-1,0:4,0:1)		!COUNT FOR AVERAGES
	REAL EAV(0:STHIFR-1,0:4,0:1)		!SUM FOR AVERAGES
	REAL*8 DAV(0:STHIFR-1,0:4,0:1)		!SUM FOR RMS
C
C  Function references:
C
C
C  Data declarations:
C
	REAL CF(0:2*STHTEL-1),CG(0:2*STHTEL-1)	!COEFFICIENTS FOR SOLUTION
	INTEGER TW1,TE1,TW2,TE2			!TELESCOPES
	REAL W2,W22				!WEIGHTS
	REAL W4,W24
	REAL R2                                 !980701
	REAL CELES(0:STHIFR-1),WCELES(0:STHIFR-1) !CELESTIAL GAINS
	REAL LSOL(0:STHTEL-1)			!LOCAL SOLUTION
	INTEGER NR				!RANK SOLUTION
	INTEGER NU				!# UNKNOWNS
	LOGICAL DOQ0				!RG1/RG2 SWITCH
	LOGICAL DOXY				!XY SIMULTANEOUS SWITCH
	LOGICAL DOSC				!SELFCAL OPTION
	LOGICAL DOAL				!ALIGN SWITCH
C-
C
C INIT
C
	NCARGS=.TRUE.					!ASSUME OK
	NU=STHTEL					!# OF UNKNOWNS
	DOXY=.FALSE.					!SEPARATE X/Y
	DOQ0=.FALSE.					!NO Q=0
	GOTO 20
C
C X/Y SIMULTANEOUS
C
	ENTRY NCARG1(MAR,NIFR,IFR,BASEL,NDEG,
	1			IRED,WGT,AWGT,CDAT,AMP,PHAS,CMOD,CWGT,
	1			CSOL,SOL,MU,ME)
C
	NCARG1=.TRUE.					!ASSUME OK
	NU=2*STHTEL					!# OF UNKNOWNS
	DOXY=.TRUE.					!COMBINE X/Y
	DOQ0=.FALSE.					!NO Q=0
	GOTO 20
C
C X/Y SIMULTANEOUS WITH Q=0
C
	ENTRY NCARG2(MAR,NIFR,IFR,BASEL,NDEG,
	1			IRED,WGT,AWGT,CDAT,AMP,PHAS,CMOD,CWGT,
	1			CSOL,SOL,MU,ME)
C
	NCARG2=.TRUE.					!ASSUME OK
	NU=2*STHTEL					!# OF UNKNOWNS
	DOXY=.TRUE.					!COMBINE X/Y
	DOQ0=.TRUE.					!Q=0
	GOTO 20
C
C SELFCAL SOLUTION
C
	ENTRY NCASGS(MAR,NIFR,IFR,BASEL,NDEG,
	1			IRED,WGT,AWGT,CDAT,AMP,PHAS,CMOD,CWGT,
	1			CSOL,SOL,MU,ME)
C
	NCASGS=.TRUE.					!ASSUME OK
	NU=STHTEL					!# OF UNKNOWNS
	DOXY=.FALSE.					!NO COMBINE X/Y
	DOQ0=.FALSE.					!NO Q=0
	DOSC=.TRUE.					!SELFCAL
	GOTO 21
C
C ALIGN SOLUTION
C
	ENTRY NCAAGS(MAR,NIFR,IFR,BASEL,NDEG,
	1			IRED,WGT,AWGT,CDAT,AMP,PHAS,CMOD,CWGT,
	1			CSOL,SOL,MU,ME,NUK,ALEQ)
C
	NCAAGS=.TRUE.					!ASSUME OK
	NU=NUK						!# OF UNKNOWNS
	DOXY=.FALSE.					!NO COMBINE X/Y
	DOQ0=.FALSE.					!NO Q=0
	DOSC=.FALSE.					!NO SELFCAL
	DOAL=.TRUE.					!ALIGN
	GOTO 22
C
C ZERO SOLUTION MATRIX
C
 20	CONTINUE
	DOSC=.FALSE.					!NOT SELFCAL
 21	CONTINUE
	DOAL=.FALSE.					!NOT ALIGN
 22	CONTINUE
	CALL WNMLIA(MAR,LSQ_I_ALL)			!FULL AREA
C
C MAKE MATRIX
C
	I1=0						!TEST REDUNDANT BASELINE
	DO I=0,NIFR-1					!ALL IFRS
	  IF (.NOT.DOAL .AND. IRED(I).GT.0) THEN	!REDUNDANT
	    IF (IRED(I).GT.I1) THEN			!NEXT SET
	      IF (WGT(I,0).GT.0 .AND. (.NOT.DOXY .OR. (DOXY .AND.
	1			WGT(I,1).GT.0))) THEN	!CAN USE AS BASE
	        I1=IRED(I)				!NEW TEST VALUE
	        I4=I
	        TE1=IFR(I)/256				!TELESCOPES
	        TW1=MOD(IFR(I),256)
	        W2=AWGT(I,0)				!SAVE WEIGHT
		IF (DOXY) W4=AWGT(I,1)
		IF (DOQ0) THEN				!Q=0
	          DO I2=0,NU-1
	            CF(I2)=0				!ZERO COEFFICIENTS
	          END DO
	          CF(TW1)=CF(TW1)+1
	          CF(STHTEL+TW1)=CF(STHTEL+TW1)-1
	          CF(TE1)=CF(TE1)+1
	          CF(STHTEL+TE1)=CF(STHTEL+TE1)-1
	          CALL WNMLMN(MAR,LSQ_C_REAL,CF,W4*W2/(W4+W2),
	1		LOG(AMP(I,0))-LOG(AMP(I,1))-
	1		CSOL(TW1,0,0)-CSOL(TE1,0,0)
	1		+CSOL(TW1,0,1)+CSOL(TE1,0,1))
		END IF
		IF (DOSC) THEN				!SELFCAL
		  W4=(CWGT(I)*ABS(CMOD(I,0)))**2	!MODEL WEIGHT
		  IF (W4.NE.0) THEN
	            DO I2=0,NU-1
	              CF(I2)=0				!ZERO COEFFICIENTS
	            END DO
	            CF(TW1)=CF(TW1)+1
	            CF(TE1)=CF(TE1)+1
	            CALL WNMLMN(MAR,LSQ_C_REAL,CF,W4*W2/(W4+W2),
	1		LOG(AMP(I,0))-LOG(ABS(CMOD(I,0)))-
	1		CSOL(TW1,0,0)-CSOL(TE1,0,0))
		  END IF
		END IF
	     	DO I3=I+1,NIFR-1				!FIND OTHERS 
	          IF (IRED(I3).EQ.I1.AND.
	1	    WGT(I3,0).GT.0 .AND. (.NOT.DOXY .OR. (DOXY .AND.
	1			WGT(I3,1).GT.0))) THEN	!CAN INCLUDE
	            TE2=IFR(I3)/256				!TELESCOPES
	            TW2=MOD(IFR(I3),256)
	            W22=AWGT(I3,0)				!WEIGHTS
		    IF (DOXY) W24=AWGT(I3,1)
	            DO I2=0,NU-1
	              CF(I2)=0				!ZERO COEFFICIENTS
	            END DO
	            CF(TW1)=CF(TW1)+1			!SET COEFFICIENTS
	            CF(TE1)=CF(TE1)+1
	            CF(TW2)=CF(TW2)-1
	            CF(TE2)=CF(TE2)-1
	            CALL WNMLMN(MAR,LSQ_C_REAL,CF,W2*W22/(W2+W22),
	1		LOG(AMP(I4,0))-LOG(AMP(I3,0))-CSOL(TW1,0,0)-
	1		CSOL(TE1,0,0)+CSOL(TW2,0,0)+CSOL(TE2,0,0))
	            IF (DOXY) THEN			!XY SIMULTANEOUS
	              DO I2=0,NU-1
	                CF(I2)=0			!ZERO COEFFICIENTS
	              END DO
	              CF(STHTEL+TW1)=CF(STHTEL+TW1)+1	!SET COEFFICIENTS
	              CF(STHTEL+TE1)=CF(STHTEL+TE1)+1
	              CF(STHTEL+TW2)=CF(STHTEL+TW2)-1
	              CF(STHTEL+TE2)=CF(STHTEL+TE2)-1
	              CALL WNMLMN(MAR,LSQ_C_REAL,CF,W4*W24/(W4+W24),
	1		LOG(AMP(I4,1))-LOG(AMP(I3,1))-CSOL(TW1,0,1)-
	1		CSOL(TE1,0,1)+CSOL(TW2,0,1)+CSOL(TE2,0,1))
		    END IF
	          END IF
	        END DO
	      END IF
	    END IF
	  ELSE IF (DOQ0 .AND. WGT(I,0).GT.0 .AND. WGT(I,1).GT.0) THEN
	    W22=AWGT(I,0)				!WEIGHTS
	    W24=AWGT(I,1)
	    TE2=IFR(I)/256				!TELESCOPES
	    TW2=MOD(IFR(I),256)
	    DO I2=0,NU-1
	      CF(I2)=0					!ZERO COEFFICIENTS
	    END DO
	    CF(TW2)=CF(TW2)+1
	    CF(STHTEL+TW2)=CF(STHTEL+TW2)-1
	    CF(TE2)=CF(TE2)+1
	    CF(STHTEL+TE2)=CF(STHTEL+TE2)-1
	    CALL WNMLMN(MAR,LSQ_C_REAL,CF,W24*W22/(W24+W22),
	1		LOG(AMP(I,0))-LOG(AMP(I,1))-
	1		CSOL(TW2,0,0)-CSOL(TE2,0,0)
	1		+CSOL(TW2,0,1)+CSOL(TE2,0,1))
	  ELSE IF (DOSC .AND. WGT(I,0).GT.0) THEN
	    W22=AWGT(I,0)				!DATA WEIGHT
	    W24=(CWGT(I)*ABS(CMOD(I,0)))**2		!MODEL WEIGHT
	    TE2=IFR(I)/256				!TELESCOPES
	    TW2=MOD(IFR(I),256)
	    IF (W24.NE.0) THEN
	      DO I2=0,NU-1
	        CF(I2)=0				!ZERO COEFFICIENTS
	      END DO
	      CF(TW2)=CF(TW2)+1
	      CF(TE2)=CF(TE2)+1
	      CALL WNMLMN(MAR,LSQ_C_REAL,CF,W22*W24/(W22+W24),
	1		LOG(AMP(I,0))-LOG(ABS(CMOD(I,0)))-
	1		CSOL(TW2,0,0)-CSOL(TE2,0,0))
	    END IF
	  ELSE IF (DOAL .AND. WGT(I,0).GT.0) THEN	!ALIGN
	    W22=AWGT(I,0)				!DATA WEIGHT
	    W24=(CWGT(I)*ABS(CMOD(I,0)))**2		!MODEL WEIGHT
	    TE2=IFR(I)/256				!TELESCOPES
	    TW2=MOD(IFR(I),256)
	    IF (W24.NE.0) THEN
	      DO I2=0,NU-1
	        CF(I2)=ALEQ(TW2,I2)+ALEQ(TE2,I2)	!SET COEFFICIENTS
	      END DO
	      CALL WNMLMN(MAR,LSQ_C_REAL,CF,W22*W24/(W22+W24),
	1		LOG(AMP(I,0))-LOG(ABS(CMOD(I,0)))-
	1		CSOL(TW2,0,0)-CSOL(TE2,0,0))
	    END IF
	  END IF
	END DO
C
C INVERT NORMAL EQUATIONS
C
	CALL WNMLID(MAR)				!FIX MISSING TELESCOPES
	CALL WNMLTR(MAR,NR)				!LU DECOMP. + RANK
	NDEG=NU-NR					!DEGENERACY
C
C SOLVE
C
	CALL WNMLSN(MAR,SOL,MU,ME)			!GET SOLUTION
	DO I=0,NU-1					!CHECK FUNNY SOLUTION
	  IF (ABS(SOL(I,0,0)).GT.5.) NCARGS=.FALSE.
	END DO
	IF (NCARGS .AND. DOAL) THEN			!MAKE ALIGN SOLUTION
	  DO I=0,NU-1					!SAVE SOLUTION
	    LSOL(I)=SOL(I,0,0)
	  END DO
	  DO I=0,STHTEL-1				!SET TELESCOPE ERRORS
	    SOL(I,0,0)=0
	    DO I1=0,NU-1
	      SOL(I,0,0)=SOL(I,0,0)+ALEQ(I,I1)*LSOL(I1)
	    END DO
	  END DO
	END IF
C
	RETURN						!READY
C
C ERROR CALCULATION
C
	ENTRY NCARGE(MAR,NIFR,IFR,BASEL,NDEG,
	1			IRED,WGT,AWGT,CDAT,AMP,PHAS,CMOD,CWGT,
	1			CSOL,SOL,MU,ME,
	1			ARMS,JAV,EAV,DAV)
C
	NCARGE=.TRUE.					!ASSUME OK
	J0=1						!ADD ERRORS
	DOSC=.FALSE.					!NOT SELFCAL
	DOAL=.FALSE.					!NO ALIGN
	GOTO 10
C
C ERROR CORRECTION
C
	ENTRY NCARGC(MAR,NIFR,IFR,BASEL,NDEG,
	1			IRED,WGT,AWGT,CDAT,AMP,PHAS,CMOD,CWGT,
	1			CSOL,SOL,MU,ME,
	1			ARMS,JAV,EAV,DAV)
C
	NCARGC=.TRUE.					!ASSUME OK
	J0=-1						!SUBTRACT ERROR
	DOSC=.FALSE.					!NOT SELFCAL
	DOAL=.FALSE.					!NO ALIGN
	GOTO 10
C
C SELFCAL ERROR CALCULATION
C
	ENTRY NCASGE(MAR,NIFR,IFR,BASEL,NDEG,
	1			IRED,WGT,AWGT,CDAT,AMP,PHAS,CMOD,CWGT,
	1			CSOL,SOL,MU,ME,
	1			ARMS,JAV,EAV,DAV)
C
	NCASGE=.TRUE.					!ASSUME OK
	J0=1						!ADD ERRORS
	DOSC=.TRUE.					!SELFCAL
	DOAL=.FALSE.					!NO ALIGN
	GOTO 10
C
C SELFCAL ERROR CORRECTION
C
	ENTRY NCASGC(MAR,NIFR,IFR,BASEL,NDEG,
	1			IRED,WGT,AWGT,CDAT,AMP,PHAS,CMOD,CWGT,
	1			CSOL,SOL,MU,ME,
	1			ARMS,JAV,EAV,DAV)
C
	NCASGC=.TRUE.					!ASSUME OK
	J0=-1						!SUBTRACT ERROR
	DOSC=.TRUE.					!SELFCAL
	DOAL=.FALSE.					!NO ALIGN
	GOTO 10
C
C ALIGN ERROR CALCULATION
C
	ENTRY NCAAGE(MAR,NIFR,IFR,BASEL,NDEG,
	1			IRED,WGT,AWGT,CDAT,AMP,PHAS,CMOD,CWGT,
	1			CSOL,SOL,MU,ME,
	1			ARMS,JAV,EAV,DAV)
C
	NCAAGE=.TRUE.					!ASSUME OK
	J0=1						!ADD ERRORS
	DOSC=.FALSE.					!NO SELFCAL
	DOAL=.TRUE.					!ALIGN
	GOTO 10
C
C ALIGN ERROR CORRECTION
C
	ENTRY NCAAGC(MAR,NIFR,IFR,BASEL,NDEG,
	1			IRED,WGT,AWGT,CDAT,AMP,PHAS,CMOD,CWGT,
	1			CSOL,SOL,MU,ME,
	1			ARMS,JAV,EAV,DAV)
C
	NCAAGC=.TRUE.					!ASSUME OK
	J0=-1						!SUBTRACT ERROR
	DOSC=.FALSE.					!NO SELFCAL
	DOAL=.TRUE.					!ALIGN
	GOTO 10
C
C GET CELESTIAL GAINS
C
 10	CONTINUE
	IF (DOSC .OR. DOAL) THEN			!SELFCAL/ALIGN
	  DO I=0,NIFR-1					!GET MODEL AMPLITUDES
	    CELES(I)=LOG(ABS(CMOD(I,0)))
	  END DO
	ELSE
	  DO I=0,NIFR-1					!ZERO CELESTIAL SOLUTION
	    CELES(I)=0
	    WCELES(I)=0
	  END DO
	  DO I=0,NIFR-1					!CALCULATE
	    IF (IRED(I).GT.0) THEN			!REDUNDANT
	      IF (WGT(I,0).GT.0) THEN			!CAN USE
	        I1=IRED(I)				!POINTER
	        TE1=IFR(I)/256				!TELESCOPES
	        TW1=MOD(IFR(I),256)
	        W2=AWGT(I,0)				!WEIGHT
	        CELES(I1)=CELES(I1)+W2*(LOG(AMP(I,0))-
	1		CSOL(TW1,0,0)-CSOL(TE1,0,0))	!SUM
	        WCELES(I1)=WCELES(I1)+W2		!SUM WEIGHT
	      END IF
	    END IF
	  END DO
	  DO I=0,NIFR-1					!SOLVE
	    IF (WCELES(I).GT.0) CELES(I)=CELES(I)/WCELES(I)
	  END DO
	END IF
C
C INIT
C
	JAV(0,0,0)=0					!NOISE
	EAV(0,0,0)=0
	DAV(0,0,0)=0
	JAV(1,0,0)=0					!FRACT. NOISE (INCONS.)
	EAV(1,0,0)=0
	DAV(1,0,0)=0
	JAV(4,0,0)=0					!MAX. DEVIATION
	EAV(4,0,0)=0
	IF (JAV(2,0,0).NE.0) THEN			!TOTAL COUNT
	  EAV(3,0,0)=SQRT(EAV(2,0,0)/JAV(2,0,0))	!OVERALL RUNNING NOISE
	ELSE
	  EAV(3,0,0)=0
	END IF
C
C DO FOR IFRS
C
	DO I=0,NIFR-1					!ALL IFRS
	  IF (DOSC .OR. DOAL .OR. IRED(I).GT.0) THEN	!REDUNDANT
	    IF (WGT(I,0).GT.0) THEN			!CAN USE
	      IF (DOSC .OR. DOAL) THEN			!SELFCAL/ALIGN
		I1=I
	      ELSE
	        I1=IRED(I)				!REDUNDANT POINTER
	      END IF
	      TE1=IFR(I)/256				!TELESCOPES
	      TW1=MOD(IFR(I),256)
C
C ERROR
C
	      R0=LOG(AMP(I,0))-CSOL(TW1,0,0)-CSOL(TE1,0,0)-CELES(I1) !ERROR
	      IF (R0.GT.10) R0=10			!LIMIT EXTREME
	      R1=(EXP(R0)-1)*AMP(I,0)			!FRACTIONAL ERROR
	      R2=AMP(I,0)                               !980701
C
C EXTREME
C
	      IF (ABS(R0).GT.ABS(EAV(4,0,0))) THEN	!MAX. DEVIATION
		EAV(4,0,0)=R0
		JAV(4,0,0)=TW1*16+TE1			!WHERE
	      END IF
C
C NOISE
C
	      JAV(0,0,0)=JAV(0,0,0)+J0			!COUNT
	      EAV(0,0,0)=EAV(0,0,0)+J0*(R1**2)		!NOISE
	      JAV(1,0,0)=JAV(1,0,0)+J0			!INCONSISTENCY
	      EAV(1,0,0)=EAV(1,0,0)+J0*(R0**2)
	      JAV(5,0,0)=JAV(5,0,0)+J0			!AVG NOISE
	      EAV(5,0,0)=EAV(5,0,0)+J0*R1
	      JAV(6,0,0)=JAV(6,0,0)+J0			!AVG INCONSISTENCY
	      EAV(6,0,0)=EAV(6,0,0)+J0*R0
	      JAV(2,0,0)=JAV(2,0,0)+J0			!AVG RMS
	      EAV(2,0,0)=EAV(2,0,0)+J0*(R1**2)
	      JAV(I,1,0)=JAV(I,1,0)+J0			!IFR AVG NOISE
	      DAV(I,1,0)=DAV(I,1,0)+J0*R1
	      EAV(I,1,0)=EAV(I,1,0)+J0*R0		!FRACT. NOISE
	      JAV(I,2,0)=JAV(I,2,0)+J0			!IFR AVG RMS
	      DAV(I,2,0)=DAV(I,2,0)+J0*(R1**2)
	      EAV(I,4,0)=EAV(I,4,0)+J0*R0*R2*R2         !980701
	      DAV(I,4,0)=DAV(I,4,0)+J0*R2*R2            !980701
	    END IF
	  END IF
	END DO
C
C NOISES AND TEL. GAINS
C
	IF (JAV(0,0,0).GT.0) THEN			!AVERAGE POSSIBLE
	  EAV(0,0,0)=SQRT(EAV(0,0,0)/JAV(0,0,0))	!NOISE
	  EAV(1,0,0)=SQRT(EAV(1,0,0)/JAV(1,0,0))	!INCONSISTENCY
	  DO I=0,STHTEL-1				!PER TELESCOPE
	    JAV(I,3,0)=JAV(I,3,0)+J0			!COUNT
	    EAV(I,3,0)=EAV(I,3,0)+J0*CSOL(I,0,0)	!AVERAGE CORRECTION
	    DAV(I,3,0)=DAV(I,3,0)+J0*CSOL(I,0,0)*CSOL(I,0,0) !GAIN RMS
	  END DO
	END IF
C
C AVERAGE AMPLITUDE
C
	JAV(7,0,0)=JAV(7,0,0)+J0			!AVERAGE AMPLITUDE
	EAV(7,0,0)=EAV(7,0,0)+J0*DBLE(ARMS(1))		!SUM
	DAV(7,0,0)=DAV(7,0,0)+J0*(DBLE(ARMS(1))**2)	!RMS
C
	RETURN
C
C
	END

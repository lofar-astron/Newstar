C+ NMOOTO.FOR
C  WNB 900827
C
C  Revisions:
C	HjV 920520	HP does not allow extended source lines
C	WNB 931006	Text
C
	SUBROUTINE NMOOTO
C
C  Convert source list to old format
C
C  Result:
C
C	CALL NMOOTO
C				Convert a source list to old format
C
C  PIN references
C
C
C  Include files:
C
	INCLUDE 'WNG_DEF'
	INCLUDE 'NMO_DEF'
	INCLUDE 'MDH_O_DEF'			!MODEL HEADER
	INCLUDE 'MDL_O_DEF'			!MODEL LINE
C
C  Entries:
C
C
C  Parameters:
C
C
C  Arguments:
C
C
C  Function references:
C
	REAL*8 WNGDNF				!ANGLE CONSTRAINT
C
C  Data declarations:
C
	BYTE MDH(0:MDHHDL-1)			!SOURCE HEADER
	  INTEGER MDHJ(0:MDHHDL/4-1)
	  REAL MDHE(0:MDHHDL/4-1)
	  REAL*8 MDHD(0:MDHHDL/8-1)
	  EQUIVALENCE (MDH,MDHJ,MDHE,MDHD)
	BYTE MDL(0:MDLHDL-1)			!SOURCE LINE
	  INTEGER MDLJ(0:MDLHDL/4-1)
	  REAL MDLE(0:MDLHDL/4-1)
	  EQUIVALENCE (MDL,MDLJ,MDLE)
	INTEGER TYPSRC				!TYPE OF SOURCE
C-
C
C INIT
C
	CALL NMOHZD(GDES)			!CLEAR HEADER
	CALL NMORDS(FCAOUT)			!READ SOURCES
	CALL NMORDM(7,-1)			!AND ADD TO GENERAL
C
C WRITE OLD FORMAT
C
	CALL WNGLUN(J1)				!UNIT #
	OPEN (UNIT=J1,ERR=900,FILE=FILIN,STATUS='NEW') !OPEN MODEL FILE
 40	CONTINUE
	WRITE (UNIT=J1,FMT=1000,ERR=910) '!'
 1000	FORMAT(A)
	WRITE (UNIT=J1,FMT=1000,ERR=910) '!'
	WRITE (UNIT=J1,FMT=1000,ERR=910) '! Note: If editing this file'//
	1		' by hand, make sure each line ends with a /'
	WRITE (UNIT=J1,FMT=1000,ERR=910) '!       On 850208 definition'//
	1		' of p.a. from N thru E, full halfwidth sizes'
	WRITE (UNIT=J1,FMT=1000,ERR=910) '!'
	IF (GDESJ(MDH_TYP_J).EQ.2) THEN		!1950
	  TYPSRC=2
	ELSE IF (GDESJ(MDH_TYP_J).EQ.3) THEN	!APPARENT
	  TYPSRC=3
	ELSE
	  TYPSRC=1
	END IF
	WRITE (UNIT=J1,FMT=1010,ERR=910) 'TYPE=',TYPSRC
 1010	FORMAT(A5,I3)				!TYPE
	IF (TYPSRC.GT.1) THEN
	  WRITE (UNIT=J1,FMT=1011,ERR=910) 'RA0=',GDESD(MDH_RA_D)*3600*360
 1011	  FORMAT(A4,F15.2)
	  WRITE (UNIT=J1,FMT=1011,ERR=910) 'DEC=',
	1		GDESD(MDH_DEC_D)*3600*360
	END IF
	WRITE (UNIT=J1,FMT=1000,ERR=910) '!'
	WRITE (UNIT=J1,FMT=1000,ERR=910) '!    AMPL            L'//
	1		'            M         LONG     SHORT'//
	2		'  P.A.  TP    ID'
	WRITE (UNIT=J1,FMT=1000,ERR=910) '!    (WU)            "'//
	1		'            "          "         "  '//
	2		'   deg'
	WRITE (UNIT=J1,FMT=1000,ERR=910) '!'
C
C WRITE SOURCES
C
 30	CONTINUE
	DO I=0,GDESJ(MDH_NSRC_J)-1		!ALL SOURCES
	  CALL WNGMV(MDLHDL,A_B(GDESJ(MDH_MODP_J)+I*MDLHDL-A_OB),MDL) !GET LINE
	  IF (MDLE(MDL_I_E).NE.0) THEN
	    IF (TYPSRC.GT.1) THEN		!CONVERT
	      CALL WNMCLM(GDESD(MDH_RA_D),GDESD(MDH_DEC_D),
	1		MDLE(MDL_L_E),MDLE(MDL_M_E),D0,D1)
	      MDLE(MDL_L_E)=WNGDNF(D0-GDESD(MDH_RA_D))*DPI2
	      MDLE(MDL_M_E)=WNGDNF(D1-GDESD(MDH_DEC_D))*DPI2
	    END IF
	    CALL NMOEXT(MDL)			!EXTERNAL FORMAT
	    I1=MDL(MDL_TP_B)
	    IF (IAND(MDLCLN_M,I1).EQ.1) THEN	!CLEAN COMPONENT
	      IF (MDLJ(MDL_ID_J).LT.3000)
	1		MDLJ(MDL_ID_J)=MDLJ(MDL_ID_J)+3000 !SET CLEAN
	    END IF
	    WRITE (UNIT=J1,FMT=1020,ERR=910)
	1		MDLE(MDL_I_E),MDLE(MDL_L_E),
	1		MDLE(MDL_M_E),MDLE(MDL_EXT_E),
	1		MDLE(MDL_EXT_E+1),MDLE(MDL_EXT_E+2),
	1		MDL(MDL_TP1_B),MDLJ(MDL_ID_J),'/'
 1020	    FORMAT(1X,F10.3,2(1X,F12.2),2(1X,F9.2),1X,F6.1,I4,I6,T77,A1)
	  END IF
	END DO
C
C END OF FILE
C
 20	CONTINUE
	CLOSE (UNIT=J1,ERR=21)			!CLOSE FILE
 21	CONTINUE
	CALL WNGLUF(J1)				!RELEASE LUN
C
	RETURN
C
C ERRORS
C
 900	CONTINUE
	CALL WNCTXT(F_TP,'Cannot open file !AS',FILIN)
	GOTO 911
C
 910	CONTINUE
	CALL WNCTXT(F_TP,'Write error')
	CLOSE (UNIT=J1,ERR=911)
 911	CONTINUE
	CALL WNGLUF(J1)
C
	RETURN
C
C
	END

C+ NSCUV0.FOR
C  WNB 910220
C
C  Revisions:
C	WNB 930819	Add Dipole code to INSTRUME; remove L_
C	CMV 940418	Correct labling of polarisations
C       CMV 961011      Write RR,LL instead of XX,YY (confused AIPS)
C       CMV 970130      Allow different BITPIXes
C
	LOGICAL FUNCTION NSCUV0(OMCA,NPOL,POLT,IATOFF,NSCN,NFRQ,
	1		FRQTB,STPTB,STH,OHW,SCW,BTP)
C
C  Write UVFITS header
C
C  Result:
C
C	NSCUV0_L = NSCUV0_L( OMCA_J:I, NPOL_J:I, POLT_J(1:*):I, 
C				IATOFF_D:I, NSCN_J:I, NFRQ_J:I,
C				FRQTB_D:I, STPTB_D:I,
C				STH_B(0:*):I, OHW_B(0:*):I, SCW_B(0:*):I,
C                               BTP_J:I)
C				will write the UVFITS header.
C				OMCA is the output file, NPOL the
C				number of polarisation channels.
C				POLT has the polarisation codes in sequence.
C				IATOFF is IAT-UTC in fractions of day
C				STH, OHW and SCW are the Set header, the OH
C				block and the SC block.
C				NSCN is the number of scans in field.
C				NFRQ the number of simultaneous frequencies
C				FRQTB the first frequency
C				STPTB the step in frequency
C                               BTP is the BIXPIX value (-32,16,32)
C
C  Include files:
C
	INCLUDE 'WNG_DEF'
	INCLUDE 'STH_O_DEF'		!SET HEADER
	INCLUDE 'OHW_O_DEF'		!OH BLOCK
	INCLUDE 'SCW_O_DEF'		!SC BLOCK
	INCLUDE 'CBITS_DEF'		!DEFINITIONS
C
C  Parameters:
C
	INTEGER LRCLEN			!RECORD LENGTH FITS (CHANGE ALSO NSCUWB)
	  PARAMETER (LRCLEN=2880)
	INTEGER CDILEN			!CARD IMAGE LENGTH
	  PARAMETER (CDILEN=80)
	INTEGER NCDI			!# OF CARD IMAGES/RECORD
	  PARAMETER (NCDI=LRCLEN/CDILEN)
	INTEGER DBLEN			!DATA BUFFER LENGTH
	  PARAMETER (DBLEN=512)		!BYTES	END IF
	INTEGER V_Z,V_L,V_I,V_J,V_C,V_E,V_D,V_T,
	1		V_XI,V_XJ	!CODES FOR FITS CARD LINES
	  PARAMETER (V_Z=0,V_L=1,V_I=2,V_J=3,V_C=4,V_E=5,V_D=6,
	1		V_T=7,V_XI=8,V_XJ=9)
C
C  Arguments:
C
	INTEGER OMCA			!FILE POINTER
	INTEGER NPOL			!# OF POL. TO DO
	INTEGER POLT(1:*)		!POLARISATION CODES
	DOUBLE PRECISION IATOFF		!IAT-UTC
	INTEGER NSCN			!# OF SCANS IN FIELD
	INTEGER NFRQ			!# OF FREQ. CHANNELS
	DOUBLE PRECISION FRQTB		!FIRST FREQUENCY
	DOUBLE PRECISION STPTB		!STEP IN FREQ.
	BYTE STH(0:*)			!SET HEADER
	BYTE OHW(0:*)			!OH BLOCK
	BYTE SCW(0:*)			!SC BLOCK
        INTEGER BTP                     !BITPIX
C
C  Function references:
C
	DOUBLE PRECISION WNGDFD,WNGDND	!ANGLE CONVERSION
	LOGICAL NSCUWF			!FILL FITS LINE
	LOGICAL NSCUMF,NSCUMS		!MAKE FITS LINE
	INTEGER WNCALN			!STRING LENGTH
	INTEGER*2 WNGGI			!GET VALUES FROM PARAMETER
	INTEGER WNGGJ
	REAL WNGGE
	DOUBLE PRECISION WNGGD
C
C  Data declarations:
C
	INTEGER*2 DATB(0:2)		!DATE BUFFER
	CHARACTER*12 TXT		!HELP AREA
        DOUBLE PRECISION BSC            !DATA SCALE
C-
C
C INIT
C
	NSCUV0=.TRUE.					!ASSUME OK
C
C WRITE STANDARD HEADER
C
	IF (.NOT.NSCUMF(OMCA,V_L,'SIMPLE',.TRUE.,
	1				'Simple type')) GOTO 910

        BSC=1D0/200.D0                                  !FIXED FOR NOW
        IF (BTP.EQ.-32) THEN
	  IF (.NOT.NSCUMF(OMCA,V_J,'BITPIX',-32,
	1			'FLOATING POINT')) GOTO 910
        ELSE IF (BTP.EQ.32) THEN
	  IF (.NOT.NSCUMF(OMCA,V_J,'BITPIX',32,
	1			'INTEGER*4')) GOTO 910
        ELSE                                            !ASSUME 16
	  IF (.NOT.NSCUMF(OMCA,V_J,'BITPIX',16,
	1			'INTEGER*2')) GOTO 910
        END IF
C
	IF (.NOT.NSCUMF(OMCA,V_J,'NAXIS',6,
	1				' ')) GOTO 910
	IF (.NOT.NSCUMF(OMCA,V_J,'NAXIS1',0,
	1				'Just groups')) GOTO 910
	IF (.NOT.NSCUMF(OMCA,V_J,'NAXIS2',3,
	1				'Real, Imag, Weight')) GOTO 910
	IF (.NOT.NSCUMF(OMCA,V_J,'NAXIS3',NPOL,
	1				'XX,YY,XY,YX')) GOTO 910
	IF (.NOT.NSCUMF(OMCA,V_J,'NAXIS4',NFRQ,
	1				'Bands')) GOTO 910
	IF (.NOT.NSCUMF(OMCA,V_J,'NAXIS5',1,
	1				'Right Ascension')) GOTO 910
	IF (.NOT.NSCUMF(OMCA,V_J,'NAXIS6',1,
	1				'Declination')) GOTO 910
C
	IF (.NOT.NSCUMF(OMCA,V_L,'EXTEND',.TRUE.,
	1				'Extension tables')) GOTO 910
	IF (.NOT.NSCUMF(OMCA,V_L,'BLOCKED',.TRUE.,
	1				'Tape maybe blocked')) GOTO 910
C
	CALL WNGMTS(12,STH(STH_FIELD_1),TXT)
	IF (.NOT.NSCUMS(OMCA,V_C,'OBJECT',TXT,
	1				'Field name')) GOTO 910
	IF (.NOT.NSCUMS(OMCA,V_C,'TELESCOP','WSRT',
	1				'Telescope')) GOTO 910
	CALL WNGMTS(4,OHW(OHW_BECODE_1),TXT)
	CALL WNCTXS(TXT(5:),'- !3$XJ',STH(STH_DIPC_1))
	IF (.NOT.NSCUMS(OMCA,V_C,'INSTRUME',TXT(1:8),
	1				'Backend code, dipole pos.')) GOTO 910
	IF (.NOT.NSCUMS(OMCA,V_C,'ORIGIN','NFRA',
	1				'Place')) GOTO 910
	CALL WNCTXS(TXT,'!8$UI',OHW(OHW_PROJECT_1))
	IF (.NOT.NSCUMS(OMCA,V_C,'OBSERVER',TXT(1:8),
	1				'Project')) GOTO 910
	DATB(2)=WNGGI(OHW(OHW_DATE_1+1*LB_I))		!DATE
	DATB(1)=WNGGI(OHW(OHW_DATE_1+2*LB_I))
	DATB(0)=WNGGI(OHW(OHW_DATE_1+3*LB_I))
	IF (.NOT.NSCUMF(OMCA,V_T,'DATE-OBS',DATB,
	1				'Date of observation')) GOTO 910
	IF (.NOT.NSCUMS(OMCA,V_C,'DATE','30/01/97',
	1				'Program date')) GOTO 910
	IF (.NOT.NSCUMF(OMCA,V_J,'BLANK',-32768,
	1				'Skipped data value')) GOTO 910
C
	IF (.NOT.NSCUMF(OMCA,V_D,'BSCALE',BSC,
	1				'Data scale')) GOTO 910
	IF (.NOT.NSCUMF(OMCA,V_D,'BZERO',0D0,
	1				'Value= tape*bscale +bzero')) GOTO 910
	IF (.NOT.NSCUMS(OMCA,V_C,'BUNIT','JY',
	1				'Visibility units')) GOTO 910
	IF (.NOT.NSCUMF(OMCA,V_E,'EPOCH',STH(STH_EPO_1),
	1				'Epoch')) GOTO 910
C
	IF (.NOT.NSCUMS(OMCA,V_C,'CTYPE2','COMPLEX',
	1				'1,2,3 = real,imag,weight')) GOTO 910
	IF (.NOT.NSCUMF(OMCA,V_E,'CRVAL2',1E0,
	1				' ')) GOTO 910
	IF (.NOT.NSCUMF(OMCA,V_E,'CDELT2',1E0,
	1				' ')) GOTO 910
	IF (.NOT.NSCUMF(OMCA,V_E,'CRPIX2',1E0,
	1				' ')) GOTO 910
	IF (.NOT.NSCUMF(OMCA,V_E,'CROTA2',0E0,
	1				' ')) GOTO 910
	IF (.NOT.NSCUMS(OMCA,V_C,'CTYPE3','STOKES',
	1				'-5,-6,-7,-8 = XX,YY,XY,YX')) GOTO 910
C
C	Polarisation
C
	IF (IAND(POLT(1),STOKES_P).EQ.0) THEN		!XX...
	   I2=-1
	   IF (IAND(POLT(1),XX_P).NE.0) THEN
	     I1=-1					!was -5
	     IF (NPOL.GT.1) THEN
	        IF (IAND(POLT(2),XY_P).NE.0) THEN
	           I2=-2
	        ELSE IF (IAND(POLT(2),YX_P).NE.0) THEN
	           I2=-3
	        END IF
	     END IF
	   ELSE IF (IAND(POLT(1),YY_P).NE.0) THEN
	     I1=-2					!was -6
	     IF (NPOL.GT.1) THEN
	        IF (IAND(POLT(2),YX_P).NE.0) I2=-2
	     END IF
	   ELSE IF (IAND(POLT(1),XY_P).NE.0) THEN
	     I1=-3					!was -7
	   ELSE IF (IAND(POLT(1),YX_P).NE.0) THEN
	     I1=-4					!was -8
	   END IF
	ELSE                                            !STOKES
	   I2=1
	   IF (IAND(POLT(1),SI_P).NE.0) THEN
	     I1=1
	     IF (NPOL.GT.1) THEN
	        IF (IAND(POLT(2),SU_P).NE.0) THEN
	           I2=2
	        ELSE IF (IAND(POLT(2),SV_P).NE.0) THEN
	           I2=3
	        END IF
	     END IF
	   ELSE IF (IAND(POLT(1),SQ_P).NE.0) THEN
	     I1=2
	     IF (NPOL.GT.1) THEN
	        IF (IAND(POLT(2),SV_P).NE.0) I2=2
	     END IF
	   ELSE IF (IAND(POLT(1),SU_P).NE.0) THEN
	     I1=3
	   ELSE IF (IAND(POLT(1),SV_P).NE.0) THEN
	     I1=4
	   END IF
	END IF
	IF (.NOT.NSCUMF(OMCA,V_E,'CRVAL3',FLOAT(I1),
	1				' ')) GOTO 910
	IF (.NOT.NSCUMF(OMCA,V_E,'CDELT3',FLOAT(I2),
	1				' ')) GOTO 910
	IF (.NOT.NSCUMF(OMCA,V_E,'CRPIX3',1E0,
	1				' ')) GOTO 910
	IF (.NOT.NSCUMF(OMCA,V_E,'CROTA3',0E0,
	1				' ')) GOTO 910
C
	IF (.NOT.NSCUMS(OMCA,V_C,'CTYPE4','FREQ',
	1				'Frequency in Hz')) GOTO 910
	IF (.NOT.NSCUMF(OMCA,V_D,'CRVAL4',
	1		(FRQTB+((NFRQ-1)/2)*STPTB)*1D6,
	1				' ')) GOTO 910
	IF (.NOT.NSCUMF(OMCA,V_D,'CDELT4',STPTB*1D6,
	1				' ')) GOTO 910
	IF (.NOT.NSCUMF(OMCA,V_E,'CRPIX4',FLOAT(((NFRQ+1)/2)),
	1				' ')) GOTO 910
	IF (.NOT.NSCUMF(OMCA,V_E,'CROTA4',0E0,
	1				' ')) GOTO 910
	IF (.NOT.NSCUMS(OMCA,V_C,'CTYPE5','RA---NCP',
	1				'RA in degrees')) GOTO 910
	IF (.NOT.NSCUMF(OMCA,V_D,'CRVAL5',WNGDFD(STH(STH_RAE_1)),
	1				' ')) GOTO 910
	IF (.NOT.NSCUMF(OMCA,V_E,'CDELT5',0E0,
	1				' ')) GOTO 910
	IF (.NOT.NSCUMF(OMCA,V_E,'CRPIX5',1E0,
	1				' ')) GOTO 910
	IF (.NOT.NSCUMF(OMCA,V_E,'CROTA5',0E0,
	1				' ')) GOTO 910
	IF (.NOT.NSCUMS(OMCA,V_C,'CTYPE6','DEC--NCP',
	1				'DEC in degrees')) GOTO 910
	IF (.NOT.NSCUMF(OMCA,V_D,'CRVAL6',WNGDND(WNGDFD(STH(STH_DECE_1))),
	1				' ')) GOTO 910
	IF (.NOT.NSCUMF(OMCA,V_E,'CDELT6',0E0,
	1				' ')) GOTO 910
	IF (.NOT.NSCUMF(OMCA,V_E,'CRPIX6',1E0,
	1				' ')) GOTO 910
	IF (.NOT.NSCUMF(OMCA,V_E,'CROTA6',0E0,
	1				' ')) GOTO 910
C
	IF (.NOT.NSCUMF(OMCA,V_L,'GROUPS',.TRUE.,
	1				'AIPS groups')) GOTO 910
	IF (.NOT.NSCUMF(OMCA,V_J,'GCOUNT',WNGGJ(STH(STH_NIFR_1))*NSCN,
	1				'# of visibility groups')) GOTO 910
	IF (.NOT.NSCUMF(OMCA,V_J,'PCOUNT',6,
	1				'# of random parameters')) GOTO 910
	IF (.NOT.NSCUMS(OMCA,V_C,'PTYPE1','UU',
	1				'U in sec')) GOTO 910
	IF (.NOT.NSCUMF(OMCA,V_D,'PSCAL1',1/DCL/10D0,
	1				' ')) GOTO 910
	IF (.NOT.NSCUMF(OMCA,V_D,'PZERO1',0D0,
	1				' ')) GOTO 910
	IF (.NOT.NSCUMS(OMCA,V_C,'PTYPE2','VV',
	1				'V in sec')) GOTO 910
	IF (.NOT.NSCUMF(OMCA,V_D,'PSCAL2',1/DCL/10D0,
	1				' ')) GOTO 910
	IF (.NOT.NSCUMF(OMCA,V_D,'PZERO2',0D0,
	1				' ')) GOTO 910
	IF (.NOT.NSCUMS(OMCA,V_C,'PTYPE3','WW',
	1				'W in sec')) GOTO 910
	IF (.NOT.NSCUMF(OMCA,V_D,'PSCAL3',1/DCL/10D0,
	1				' ')) GOTO 910
	IF (.NOT.NSCUMF(OMCA,V_D,'PZERO3',0D0,
	1				' ')) GOTO 910
	IF (.NOT.NSCUMS(OMCA,V_C,'PTYPE4','BASELINE',
	1				'256*west + east')) GOTO 910
	IF (.NOT.NSCUMF(OMCA,V_D,'PSCAL4',1D0,
	1				' ')) GOTO 910
	IF (.NOT.NSCUMF(OMCA,V_D,'PZERO4',0D0,
	1				' ')) GOTO 910
	IF (.NOT.NSCUMS(OMCA,V_C,'PTYPE5','DATE',
	1				'Time in JD since 01/01/80')) GOTO 910
	IF (.NOT.NSCUMF(OMCA,V_D,'PSCAL5',1D0,
	1				' ')) GOTO 910
	IF (.NOT.NSCUMF(OMCA,V_D,'PZERO5',2.4442395D+6,
	1				' ')) GOTO 910
	IF (.NOT.NSCUMS(OMCA,V_C,'PTYPE6','DATE',
	1				'Time in JD in 10s')) GOTO 910
	IF (.NOT.NSCUMF(OMCA,V_D,'PSCAL6',1D0/24D0/360D0,
	1				' ')) GOTO 910
	IF (.NOT.NSCUMF(OMCA,V_D,'PZERO6',IATOFF,
	1				' ')) GOTO 910
CC	IF (.NOT.NSCUMS(OMCA,V_C,'PTYPE7','FREQSEL',
CC	1				'FQ frequency id')) GOTO 910
CC	IF (.NOT.NSCUMF(OMCA,V_D,'PSCAL7',1D0,
CC	1				' ')) GOTO 910
CC	IF (.NOT.NSCUMF(OMCA,V_D,'PZERO7',0D0,
CC	1				' ')) GOTO 910
C
	IF (.NOT.NSCUMF(OMCA,V_Z,'HISTORY',J,
	1				'------------------------')) GOTO 910
	CALL WNCTXS(TXT,'!UJ',STH(STH_VNR_1))
	IF (.NOT.NSCUMF(OMCA,V_Z,'HISTORY',J,
	1				'SEQUENCE # = '//TXT)) GOTO 910
	CALL WNCTXS(TXT,'!EAF12.1',STH(STH_PHI_1))
	IF (.NOT.NSCUMF(OMCA,V_Z,'HISTORY',J,
	1				'ROTATION BY '//
	1				TXT(1:WNCALN(TXT))//' DEG')) GOTO 910
	DO I=1,WNGGI(SCW(SCW_NRPNCH_1))			!WSRT COMMENTS
	  CALL WNGMTS(80,SCW(SCW_PNCHI_1+(I-1)*80),TXT)
	  IF (.NOT.NSCUMF(OMCA,V_Z,'HISTORY',J,
	1				'WSRT: '//TXT)) GOTO 910
	END DO
	IF (.NOT.NSCUMF(OMCA,V_Z,'HISTORY',J,
	1				'WEIGHT: TIME/Tsys '//
	1				'NORMALISED PER SCAN')) GOTO 910
	IF (WNGGI(OHW(OHW_MSPAT_1)).NE.0) THEN
	  CALL WNCTXS(TXT,'!UI',OHW(OHW_MSPAT_1))
	  IF (.NOT.NSCUMF(OMCA,V_Z,'HISTORY',J,
	1				'MOSAIC PATTERN '//TXT)) GOTO 910
	  CALL WNCTXS(TXT,'!UI',OHW(OHW_MPOSN_1))
	  IF (.NOT.NSCUMF(OMCA,V_Z,'HISTORY',J,
	1				'MOSAIC POINTING '//TXT)) GOTO 910
	END IF
	IF (.NOT.NSCUMF(OMCA,V_Z,'END',J,
	1				' ')) GOTO 910
	IF (.NOT.NSCUWF(OMCA)) GOTO 910			!FILL RECORD
C
C END
C
	GOTO 900
C
C ERROR
C
 910	CONTINUE
	NSCUV0=.FALSE.
 900	CONTINUE
C
	RETURN						!READY
C
C
	END

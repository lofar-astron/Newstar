#  ntarz.ssc
# 	WNB 921117
# 
#  Revisions:
#	WNB 921218	Add SSC
#	WNB 921222	Add COM
#	WNB 921224	Make SSC
#	WNB 930303	NSTAR_DIR added
#	WNB 930305	Make sure aliases
#	WNB 930514	Correct aliases
#	HjV 930914	Typo
#	WNB 940124	Leave _TLB
# 
# 	Build Newstar export files
#
#	Uses environment variables:
#		WNG			where to find wng-type sources
#		WNG_TYPE		machine (sw, dw, hp, al, cv etc)
#		WNG_OLBEXE		root of wng-type .olb, .ppd, .exe
#		WNG_SITE		site (nfra, atnf, rug ...)
#		EXEDWARF_UNIX		where to find/set DWARF .exe, .ppd
#		LIBDWARF		where to find DWARF .olb
#	and also possible:
#		WNG_EXE, WNG_OLB, WNG_TLB, WNG_ERR, WNG_LIS, WNG_LINK
#
# Intro
#
	onintr exex					# finish neatly
	if ($?echo) unset echo				# no verify
	if ($?verbose) unset verbose			# no verbose
	set pid=$$ ; @ dep=1				# for tmp files
	set date=(`date`)				# get version
	if ("$date[3]" =~ [1-9]) set date[3] = "0$date[3]" # day
	set loo=( Jan Feb Mar Apr May Jun Jul Aug Sep Oct Nov Dec)
	foreach mm ( 01 02 03 04 05 06 07 08 09 10 11 12)
	  if ("$date[2]" == "$loo[$mm]") break		# month
	end
	@ yy = $date[$#date] -  1900			# year
	set ldat="$yy$mm$date[3]"			#version yymmdd
	echo " "
	echo "Building Newstar tar files."
	echo " "
	echo 'A log will be made in $WNG'"/../nt$pid$dep.log"
	echo "There should be about 50 Mbytes available,"
	echo "and it will probably take up to an hour."
	echo " "
#
# Check environment
#
	if (! $?WNG || ! $?WNG_TYPE || ! $?WNG_OLBEXE ||\
			! $?WNG_SITE || ! $?NSTAR_DIR) then
	  echo " Error: Must have environment variables"
	  echo "        WNG, WNG_TYPE, WNG_OLBEXE, WNG_SITE, NSTAR_DIR defined"
	  echo "        You probably have not included the proper"
	  echo "        files in .cshrc"
	  goto exex
	endif
	if (! $?EXEDWARF_UNIX || ! $?LIBDWARF) then
	  echo " Error:   Cannot do everything with EXEDWARF_UNIX and/or"
	  echo "          LIBDWARF not defined"
	  echo "          You probably have not included the proper"
	  echo "          files in .cshrc"
	  goto exex
	endif
        source $WNG/wngcshrc.sun			# get aliases
	if ("`alias nxec`" == "") then
	  echo " Error:   You have no alias for nxec etc."
	  echo "          You probably have not included the proper"
	  echo "          files in .cshrc"
	  goto exex
	endif
#
# Creating environment
#
	cd $WNG
	if (! $?WNG_EXE) setenv WNG_EXE $WNG_OLBEXE	# make names
	if (! $?WNG_OLB) setenv WNG_OLB $WNG_OLBEXE
	set blddir=($NSTAR_DIR)				# Newstar directories
	foreach i ($blddir)
	  if (! -d $WNG_EXE/$i) mkdir $WNG_EXE/$i	# make directories
	  if (! -d $WNG_OLB/$i) mkdir $WNG_OLB/$i
	endif
##	if ($?WNG_TLB) unsetenv WNG_TLB			# make sure
	if ($?WNG_ERR) unsetenv WNG_ERR
	if ($?WNG_LIS) unsetenv WNG_LIS
	if ($?WNG_LINK) unsetenv WNG_LINK
#
# Get questions
#
	echo "nt$pid$dep.log" >>! $WNG/../nt$pid$dep.log # start log
	echo "Running NTARZ.SUN at $WNG_SITE($WNG_TYPE)" \
			| tee -a $WNG/../nt$pid$dep.log
	echo "on `hostname` at `date`" | tee -a $WNG/../nt$pid$dep.log
	echo " " | tee -a $WNG/../nt$pid$dep.log
	set dosrc
	echo -n "Do you want the source files? (y|n) [y]: " \
			| tee -a $WNG/../nt$pid$dep.log
	set l0=($<)
	echo "$l0" >>! $WNG/../nt$pid$dep.log
	if ("$l0" =~ [nN]*) unset dosrc			# skip sources
	echo -n "Do you want the object libraries? (y|n) [n]: " \
			| tee -a $WNG/../nt$pid$dep.log
	set l0=($<)
	echo "$l0" >>! $WNG/../nt$pid$dep.log
	if ("$l0" =~ [yY]*) set doolb			# do olb
	echo -n "Do you want the executables? (y|n) [n]: " \
			| tee -a $WNG/../nt$pid$dep.log
	set l0=($<)
	echo "$l0" >>! $WNG/../nt$pid$dep.log
	if ("$l0" =~ [yY]*) set doexe			# do exe
#
# Do sources
#
DSRC:
	cd $WNG/..					# base directory
	if ($?dosrc) then
	  echo "Creating Newstar_src_unix_$ldat" \
			| tee -a $WNG/../nt$pid$dep.log
	  foreach i ($blddir)				# get sources
	    if ("$i" == "wng") then
	      'mv' ./$i/readme.txt README >& /dev/null
	      'mv' ./$i/quest.txt QUEST >& /dev/null
	      tar cvf Newstar_src_unix_$ldat README QUEST \
				>>&! $WNG/../nt$pid$dep.log
	    endif
	    tar rvf Newstar_src_unix_$ldat ./$i/*.sun ./$i/*.tlb \
				./$i/*.com ./$i/*.txt ./$i/*.hlp \
				>>&! $WNG/../nt$pid$dep.log
	  end
	  'rm' Newstar_src_unix_$ldat.Z >& /dev/null	# make sure
	  compress Newstar_src_unix_$ldat >>&! $WNG/../nt$pid$dep.log
	endif
#
# Write Object libraries
#
DOLB:
	cd $WNG_OLB					# base directory
	if ($?doolb) then
	  echo "Creating Newstar_olb_${WNG_TYPE}_$ldat" \
			| tee -a $WNG/../nt$pid$dep.log
	  foreach i ($blddir)				# get sources
	    if ("$i" == "wng") then
	      tar cvf $WNG/../Newstar_olb_${WNG_TYPE}_$ldat ./$i/*.olb ./$i/*.exe \
				>>&! $WNG/../nt$pid$dep.log
	    else
	      tar rvf $WNG/../Newstar_olb_${WNG_TYPE}_$ldat ./$i/*.olb \
				>>&! $WNG/../nt$pid$dep.log
	    endif
	  end
	  'rm' $WNG/../Newstar_olb_${WNG_TYPE}_$ldat.Z >& /dev/null # make sure
	  compress $WNG/../Newstar_olb_${WNG_TYPE}_$ldat \
			>>&! $WNG/../nt$pid$dep.log
	endif
#
# Write executables
#
DEXE:
	cd $EXEDWARF_UNIX				# base directory
	if ($?doexe) then
	  echo "Creating Newstar_exe_${WNG_TYPE}_$ldat" \
			| tee -a $WNG/../nt$pid$dep.log
	  tar cvf $WNG/../Newstar_exe_${WNG_TYPE}_$ldat ./*.exe ./*.ppd \
				>>&! $WNG/../nt$pid$dep.log
	  'rm' $WNG/../Newstar_exe_${WNG_TYPE}_$ldat.Z >& /dev/null # make sure
	  compress $WNG/../Newstar_exe_${WNG_TYPE}_$ldat \
			>>&! $WNG/../nt$pid$dep.log
	endif
#
# Cleanup
#
CLUP:
#
# Ready
#
END:
	echo " " \
			| tee -a $WNG/../nt$pid$dep.log
	echo "Copy the produced .Z files by ftp (binary) to" \
			| tee -a $WNG/../nt$pid$dep.log
	echo "rzmvx4.astron.nl user5:[wnb.wng]" \
			| tee -a $WNG/../nt$pid$dep.log
	echo "The produced Z files can be after uncompress and" \
			| tee -a $WNG/../nt$pid$dep.log
	echo "tar -xvf in the directories" \
			| tee -a $WNG/../nt$pid$dep.log
	echo '$WNG/.. for src, $WNG_OLBEXE for olb and $EXEDWARF_UNIX for exe' \
			| tee -a $WNG/../nt$pid$dep.log
	echo "be used in building the Newstar system by running" \
			| tee -a $WNG/../nt$pid$dep.log
	echo '$WNG/nbuild.sun' \
			| tee -a $WNG/../nt$pid$dep.log
	echo " " \
			| tee -a $WNG/../nt$pid$dep.log
#
#  EXIT
#
exex:
	exit

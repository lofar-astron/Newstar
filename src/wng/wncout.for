C+ WNCOUT.FOR
C  WNB 890203
C
C  Revisions:
C	WNB 900822	Limit line length
C	WNB 911115	DW: Concatenation problem
C	WNB 911230	DW: typo
C	HjV 930205	HP: type *,xxx  wraps at 70 characters
C	WNB 931025	Add User/Host to header line
C	JPH 951031	Call GEN_FLUSH for terminal output (i.e. make stdout
C			 output unbuffered)
C       HjV 960618	Call TFLUSH iso. GEN_FLUSH
C
C
	SUBROUTINE WNCOUT(COD,OUT,PBEG,PEND)
C
C  Output a WRCTXT line
C
C  Result:
C
C	CALL WNCOUT ( COD_J:I, OUT_C*:I, PBEG_J:I, PEND_J:I)
C					Output a line from WRCTXT to files
C					specified in COD. The line to be
C					written is OUT(PBEG:PEND).
C
C  Include files:
C
	INCLUDE 'WNG_DEF'
	INCLUDE 'WNC_DEF'
C
C  Parameters:
C
	CHARACTER*1 FF		!FF
C	PARAMETER (FF=CHAR(12)) !Not accepted anymore
C
C  Arguments:
C
	INTEGER COD		!FILE CODE
	CHARACTER*(*) OUT	!OUTPUT STRING
	INTEGER PBEG		!BEGIN POINTER
	INTEGER PEND		!END POINTER
C
C  Function references:
C
	INTEGER WNCALN		!STRING LENGTH
C
C  Data declarations:
C
	CHARACTER*1 PREF	!PREFIX
	CHARACTER*8 USER	!LOCAL USER
	CHARACTER*8 HOST	!LOCAL HOST
C-
	FF = CHAR(12)
	IF (IAND(COD,F_P1).NE.0) THEN		!SET PREFIX
	  PREF='>'
	ELSE
	  PREF=' '
	END IF
	J2=PBEG					!START IN STRING
	IF (OUT(J2:J2).EQ.FF) THEN		!ONLY FORMFEED
	  J=COD					!COPY CODE
	  I=-1					!FILE COUNT
	  DO WHILE (IAND(J,F_ALL).NE.0)		!ALL FILES
	    IF (I.EQ.-1) THEN			!SELECT CODE
	      J1=F_T
	    ELSE IF (I.EQ.0) THEN
	      J1=F_P
	    ELSE
	      J1=ISHFT(F_0,I-1)
	    END IF
	    IF (IAND(J,J1).NE.0) CLC(I)=0	!SET NEW PAGE
	    J=IAND(J,NOT(J1))			!DELETE BIT
	    I=I+1				!COUNT
	  END DO
C
	  RETURN				!READY
	ENDIF
C
C OUTPUT LINE
C
	J=COD					!COPY CODE
	I=-1					!FILE COUNT
	DO WHILE (IAND(J,F_ALL).NE.0)		!ALL FILES
	  IF (I.EQ.-1) THEN			!SELECT CODE
	    J1=F_T
	  ELSE IF (I.EQ.0) THEN
	    J1=F_P
	  ELSE
	    J1=ISHFT(F_0,I-1)
	  END IF
	  IF (IAND(J,J1).NE.0) THEN		!SELECTED FILE
	   IF (CLUN(I).EQ.0) CALL WNCFOP(J1,' ') !OPEN IF NECESSARY
C
C HEADINGS
C
	   IF (CLC(I).EQ.0 .AND. CPL(I).GT.CHPH(I)) THEN !NEW PAGE
	    IF (I.EQ.0 .OR. (I.GT.0 .AND. CHPH(I).GT.0)) THEN !HEADERS
	      CPC(I)=CPC(I)+1			!PAGE COUNT
	      IF (CLL(I).GE.16) THEN		!CAN SET START
		CPH(1,I)(1:16)=PRGNAM(1:WNCALN(PRGNAM))//'/'//
	1			PRGVER		!SET VERSION
	      END IF
		CALL WNGSGU(USER)		!USER
		CALL WNGSGH(HOST)		!HOST
		I3=WNCALN(USER)			!STRING LENGTHS
		I4=WNCALN(HOST)
	      IF (CLL(I).GE.29+3+I3+I4) THEN	!CAN SET USER/HOST
		CPH(1,I)(CLL(I)-29-2-I4-I3:CLL(I)-29-2-I4-I3)='/' !SEPARATORS
		CPH(1,I)(CLL(I)-29-1-I4:CLL(I)-29-1-I4)='/'
		CPH(1,I)(CLL(I)-29:CLL(I)-29)='/'
		CPH(1,I)(CLL(I)-29-1-I4-I3:CLL(I)-29-2-I4)=USER !SET USER
		CPH(1,I)(CLL(I)-29-I4:CLL(I)-29-1)=HOST !SET HOST
	      END IF
	      IF (CLL(I).GE.29) THEN		!CAN SET DATE/TIME
		CALL WNGSYT(CPH(1,I)(CLL(I)-28:CLL(I)-9)) !SET DATE/TIME
	      END IF
	      IF (CLL(I).GE.9) THEN		!CAN SET PAGE
		WRITE (UNIT=CPH(1,I)(CLL(I)-8:CLL(I)),FMT=1000,ERR=10)
	1			CPC(I)		!PAGE NUMBER
 10		CONTINUE
	      END IF
	      IF (I.EQ.0) THEN			!LOG
		WRITE (UNIT=CLUN(I),FMT=1010,ERR=11) FF
		WRITE (UNIT=CLUN(I),FMT=1010,ERR=11)
	1			' '//CPH(1,I)(1:CLL(I)) !FIRST HEADING LINE
	      ELSE
		WRITE (UNIT=CLUN(I),FMT=1010,ERR=11) FF
		WRITE (UNIT=CLUN(I),FMT=1010,ERR=11)
	1			CPH(1,I)(1:CLL(I)) !FIRST HEADING LINE FILE
	      END IF
	      CLC(I)=CLC(I)+1			!COUNT LINE
	    ELSE IF (I.GT.-1) THEN		!NEW PAGE
	      WRITE (UNIT=CLUN(I),FMT=1010,ERR=11) FF
	    END IF
 11	    CONTINUE
	    IF (I.EQ.0) THEN			!LOG
	      I1=MAX(3,CHPH(I))
	    ELSE				!FILE
	      I1=CHPH(I)			!SET LINES TO DO
	    END IF
	    DO I2=2,I1				!REMAINING HEADER LINES
	      IF (I.EQ.0) THEN			!LOG
		WRITE (UNIT=CLUN(I),FMT=1010,ERR=12)
	1			' '//CPH(I2,I)(1:CLL(I)) !HEADING LINE LOG
	      ELSE
		WRITE (UNIT=CLUN(I),FMT=1010,ERR=12)
	1			CPH(I2,I)(1:CLL(I)) !HEADING LINE FILE
	      END IF
	      CLC(I)=CLC(I)+1			!COUNT LINE
 12	      CONTINUE
	    END DO
	   END IF
C
C PRINT LINE
C
	   IF (I.EQ.-1) THEN			!SCREEN
	    I2=PBEG+MIN(PEND-PBEG,CLL(I)-1)
C	    TYPE 1010,' ',OUT(PBEG:I2)          !TYPE not allowed anymore
            WRITE(*,'(A,A)') " ",OUT(PBEG:I2)
	    CALL TFLUSH()
 	   ELSE IF (I.EQ.0) THEN		!LOG
	    I2=PBEG+MIN(PEND-PBEG,CLL(I)-2)
	    WRITE (UNIT=CLUN(I),FMT=1010,ERR=13) PREF,OUT(PBEG:I2)
	   ELSE
	    I2=PBEG+MIN(PEND-PBEG,CLL(I)-1)
	    WRITE (UNIT=CLUN(I),FMT=1010,ERR=13) OUT(PBEG:I2)
	   END IF
	   CLC(I)=CLC(I)+1			!COUNT LINE
 13	   CONTINUE
	   IF (CLC(I).GE.CPL(I) .AND. CPL(I).NE.0) CLC(I)=0 !NEW PAGE
	  END IF				!END SELECTED FILE
	  J=IAND(J,NOT(J1))			!DELETE BIT
	  I=I+1					!COUNT
	END DO
C
	RETURN
C
 1000	FORMAT(' Page',I4)
 1010	FORMAT(A,A)
C
C
	END

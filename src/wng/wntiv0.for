C+ WNTIV0.FOR
C  WNB 930501
C
C  Revisions:
C
	LOGICAL FUNCTION WNTIV0(OP,NOP,STK,NSTK,LOP,HOP)
C
C  Help routines for WNTIVG
C
C  Result:
C
C	WNTIV0_L = WNTIV0( OP_J(0:*):I, NOP_J:IO, STK_J(0:*):IO,
C				NSTK_J:IO, LOP_J:I, HOP_J:I)
C				Unroll operator (OP) and value (STK) stacks
C				for operators between LOP and HOP (inclusive).
C				Binary operators
C	WNTIV1_l = WNTIV1( OP_J(0:*):I, NOP_J:IO, STK_J(0:*):IO,
C				NSTK_J:I, LOP_J:I, HOP_J:I)
C				Unary operators
C
C  Include files:
C
	INCLUDE 'WNG_DEF'
	INCLUDE 'WNT_O_DEF'
C
C  Parameters:
C
C
C  Arguments:
C
	INTEGER OP(0:*)			!OPERATOR STACK
	INTEGER NOP			!# IN OP
	INTEGER STK(0:*)		!VALUE STACK
	INTEGER NSTK			!# IN STK
	INTEGER LOP			!LOWEST TO DO
	INTEGER HOP			!HIGHEST OPERATOR TO DO
C
C Entry points:
C
	LOGICAL WNTIV1
C
C  Function references:
C
C
C  Data declarations:
C
C-
C
C WNTIV0
C
	WNTIV0=.TRUE.				!ASSUME OK
	DO WHILE (NOP.GT.0)			!MORE OPERATORS
	  IF (OP(NOP-1).GE.LOP .AND. OP(NOP-1).LE.HOP) THEN !DO
	    IF (NSTK.LE.1) GOTO 900		!CANNOT DO
	    NOP=NOP-1				!REMOVE OPERATOR
	    NSTK=NSTK-1				!REMOVE VALUE
	    IF (OP(NOP).EQ.OP_PL) THEN
	      STK(NSTK-1)=STK(NSTK-1)+STK(NSTK)
	    ELSE IF (OP(NOP).EQ.OP_MI) THEN
	      STK(NSTK-1)=STK(NSTK-1)-STK(NSTK)
	    ELSE IF (OP(NOP).EQ.OP_MU) THEN
	      STK(NSTK-1)=STK(NSTK-1)*STK(NSTK)
	    ELSE IF (OP(NOP).EQ.OP_DV) THEN
	      IF (STK(NSTK).EQ.0) GOTO 900
	      STK(NSTK-1)=STK(NSTK-1)/STK(NSTK)
	    ELSE
	      GOTO 900				!UNKNOWN
	    END IF
	  ELSE
	    GOTO 800				!READY
	  END IF
	END DO
	GOTO 800
C
C WNTIV1
C
	ENTRY WNTIV1(OP,NOP,STK,NSTK,LOP,HOP)
C
	WNTIV1=.TRUE.				!ASSUME OK
	DO WHILE (NOP.GT.0)			!MORE OPERATORS
	  IF (OP(NOP-1).GE.LOP .AND. OP(NOP-1).LE.HOP) THEN !DO
	    IF (NSTK.LE.0) GOTO 900		!CANNOT DO
	    NOP=NOP-1				!REMOVE OPERATOR
	    IF (OP(NOP).EQ.OP_SP) THEN		!EFFECTIVE NOP
	    ELSE IF (OP(NOP).EQ.OP_SM) THEN
	      STK(NSTK-1)=-STK(NSTK-1)
	    ELSE
	      GOTO 900				!UNKNOWN
	    END IF
	  ELSE
	    GOTO 800				!READY
	  END IF
	END DO
	GOTO 800
C
C ERROR
C
 900	CONTINUE
	WNTIV0=.FALSE.				!ERROR
C
C READY
C
 800	CONTINUE
C
	RETURN
C
C
	END

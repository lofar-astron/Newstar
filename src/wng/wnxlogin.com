$ ! WNXLOGIN.COM
$ ! WNB 920127
$ !
$ ! Revisions:
$ !
$ ! General LOGIN.COM for use of WNB and WIM and BROUW and ...
$ !	P1	Name of main directory
$ !	P2	NDIR (for new subdirectory) or blank
$ !	P3	Subdirectory (if P2 NDIR) or blank
$ !	P4	VERIFY or blank
$ !
$ ! SOME STANDARDS
$ !
$	IF P4 .EQS. "" THEN LOCA=F$VERIFY(0)
$	IF P4 .NES. "" THEN LOCA=F$VERIFY(1)
$	IF P2 .EQS. "NDIR" THEN GOTO LX1		!NDIR
$	SET PROTECTION=(G:RWE,W:RE)/DEFAULT
$	IF F$ENVIRONMENT("INTERACTIVE") .EQS. "TRUE" THEN -
		SET TERM TT /VT100/FULLDUP
$	IF P1 .EQS. "BROUW" THEN EXIT
$ !
$ ! KEYS
$ !
$	DEFINE/KEY/NOLOG F20 "MONITOR SYSTEM/INTER=1" /TERMINATE/NOECHO/ERASE
$	DEFINE/KEY/NOLOG F19 "MONITOR PROCESS/TOPCPU/INTER=1" /TERM/NOECH/ERASE
$	DEFINE/KEY/NOLOG F18 "RUN SYS$MANAGER:MONPROCES" /TERM/NOECH/ERASE
$	DEFINE/KEY/NOLOG F17 "NCP TELL RZMVX5 SHO MODU X25-P KNO DTE" /TERM/NOECH/ERASE
$	DEFINE/KEY/NOLOG HELP "HELP/NOINSTR/NOPAGE" /TERM/NOECH/ERASE
$	DEFINE/KEY/NOLOG DO "VALL" /TERM/NOECH/ERASE
$	DEFINE/KEY/NOLOG E1 "SHOW SYSTEM" /TERM/NOECH/ERASE
$	DEFINE/KEY/NOLOG E2 "SHOW QUEUE/DEVICE/ALL" /TERM/NOECHO/ERASE
$	DEFINE/KEY/NOLOG E3 "SHOW QUEUE/BATCH/ALL" /TERM/NOECHO/ERASE
$	DEFINE/KEY/NOLOG E4 "SHOW KEY/ALL" /TERM/NOECHO/ERASE
$	DEFINE/KEY/NOLOG E5 -
	  "@''F$LOGICAL("SYS$DISK")'[WIM.GEN]WATCHDOG.COM" /TERM/NOECHO/ERASE
$	DEFINE/KEY/NOLOG E6 "SHV" /TERM/NOECHO/ERASE
$ !
$ ! STANDARD AREAS
$ !
$ LX1:	SET DEF SYS$LOGIN:
$	QWIM="USER5:"
$	QD=F$TRNLNM("SYS$DISK")
$	QT=QD+"[WIM.GEN]"
$ !
$ ! GET SUBDIRECTORY
$ !
$	IF P2 .EQS. "NDIR" .AND. P3 .NES. ""
$	THEN
$	  SUBNAME=P3
$	ELSE
$	  INQUIRE SUBNAME Subname
$	ENDIF
$	IF SUBNAME .EQS. "" THEN GOTO LA1
$ LA2:	DEFUNUS=F$TRNLNM("SYS$DISK")+"["+P1+"."+SUBNAME+"]"
$	GOTO LA3
$ LA1:	DEFUNUS=F$TRNLNM("SYS$DISK")+"["+P1+"]"
$ LA3:	IF P2 .NES. "" THEN GOTO LX2		!NDIR
$ !
$ ! EDITOR
$ !
$	EDT=="EDI/EDT/COMMAND=''QT'EDTINI.COM"
$	EVE=="EDI/TPU"
$ !
$ ! LOGOUT
$ !
$	BYE=="''LO'"
$	BBYE=="@''QT'BBYEALL"
$	LO*GOUT=="LOGOUT/BRIEF"
$	
$ !
$ ! STANDARD PROGRAMS
$ !
$	WC*OMPILE=="@"+F$TRNLNM("RUNDWARF")-"]"+".MNT]WCOMP %"
$	WL*INK=="@"+F$TRNLNM("RUNDWARF")-"]"+".MNT]WLINK %"
$	WD*EL=="@"+F$TRNLNM("RUNDWARF")-"]"+".MNT]WDEL %"
$	WX*REF=="@"+F$TRNLNM("RUNDWARF")-"]"+".MNT]WXREF %"
$	ADV*ENT=="RUN ''QWIM'[WIM.ADV]ADVENT"
$	CLC=="RUN ''QWIM'[WIM.CLC]CLC"
$	SDA=="$SDA"
$	TUP*DATE=="@''QT'UPDSUB [''P1'...]"
$	FDU*MP=="RUN [DWARF]PRTDSK"
$	NCP=="$NCP"
$	DTE=="$NCP TELL RZMVX5 SHO MODU X25-PROT KNOWN DTE"
$ !
$ !SET HOST
$ !
$	VX2=="SET HOST RZMVX2"
$	VX1=="SET HOST RZMVX1"
$	VALL=="SET HOST RZMALL"
$	VX3=="SET HOST RZMVX3"
$	VX4=="SET HOST RZMVX4"
$	VX5=="SET HOST RZMVX5"
$	VSUR=="SET HOST RZMSUR"
$ !
$ ! SHOW
$ !
$ LA:	SHB*ATCH=="SHOW QUEUE/BATCH/ALL"
$	SHE*NTRY=="SHOW ENTRY"
$	SHP*RINTER=="SHOW QUEUE/DEVICE/ALL"
$	SHT*IME=="SHOW TIME"
$	SHK*EY=="SHOW KEY/ALL"
$	SHM*EMORY=="SHOW MEMORY"
$	SHS*YSTEM=="SHOW SYSTEM"
$	SHD*EFAULT=="SHOW DEFAULT"
$	SHQ*UOTA=="SHOW QUOTA"
$	SHC*TATUS=="SHOW STATUS"
$	SHN*ET=="SHOW NET"
$	SHU*SERS=="SHOW USERS/FULL"
$	SHV=="@''QWIM'[WIM.GEN]SHV"
$	USE*RS=="@SYST1:[SYSMGR]USERS"
$ !
$ ! STOP
$ !
$	STPP*RINTER=="STOP/ABORT"
$ !
$ ! MONITOR
$ !
$	MND*ECNET=="MONITOR DECNET/INTERVAL=1"
$	MNF*CP=="MONITOR FCP/INTERVAL=1"
$	MNI*O=="MONITOR IO/INTERVAL=1"
$	MNL*OCK=="MONITOR LOCK/INTERVAL=1"
$	MNM*ODES=="MONITOR MODES/INTERVAL=1"
$	MNPA*GE=="MONITOR PAGE/INTERVAL=1"
$	MNPO*OL=="MONITOR POOL/INTERVAL=1"
$	MNPR*OCES=="RUN SYS$MANAGER:MONPROCES"
$	MNSY*STEM=="MONITOR SYSTEM/INTERVAL=1"
$	MNTC*PU=="MONITOR PROC/TOPCPU/INTERVAL=1"
$	MNTB*IO=="MONITOR PROC/TOPBIO/INTERVAL=1"
$	MNTD*IO=="MONITOR PROC/TOPDIO/INTERVAL=1"
$	MNTF*AULT=="MONITOR PROC/TOPFAULT/INTERVAL=1"
$	MNST*ATES=="MONITOR STATES/INTERVAL=1"
$ !
$ ! SETS
$ !
$ LX2:	STD*EF=="SET DEF ''DEFUNUS'"
$	IF P2 .NES. "" THEN GOTO LX3		!NDIR
$	STMA*IN=="SET DEF ''F$LOGICAL("SYS$DISK")'[''P1']"
$	STW*IM=="SET DEF ''QT'"
$	SWIM=="SET DEF ''QT'"
$	STS*YS=="SET DEF SYS$COMMON:[SYSEXE]"
$	STM*GR=="SET DEF SYS$COMMON:[SYSMGR]"
$	STH*ELP=="SET DEF SYS$COMMON:[SYSHLP]"
$	STE*RRORLOG=="SET DEF SYS$COMMON:[SYSERR]"
$	STL*IBRARY=="SET DEF SYS$COMMON:[SYSLIB]"
$	STPRV=="SET PROCESS/PRIV=ALL"
$	STPRI=="SET PROCESS/PRIOR=10"
$ !
$ !ASSIGNS
$ !
$	ASSIGN/NOLOG 'F$LOGICAL("SYS$SYSTEM")' ZS
$	ASSIGN/NOLOG SYS$COMMON:[SYSMGR] ZM
$	ASSIGN/NOLOG SYS$COMMON:[SYSHLP] ZH
$	ASSIGN/NOLOG 'QT' USERWIM
$ LX3:	ASSIGN/NOLOG 'DEFUNUS' USERA
$	ASSIGN/NOLOG 'DEFUNUS' CDEF
$	IF F$TRNLNM("EMAIL_NAME") .EQS. "" THEN -
			ASSIGN/NOLOG "Dr. W.N. Brouw" EMAIL_NAME
$	IF P2 .NES. "" THEN GOTO LX4		!NDIR
$ !
$ ! DIRECTORY HANDLING
$ !
$	ND*IRECT=="@''QT'LOGINALL ''P1' NDIR"
$      DDIR*ECTORY=="DIRECT/DATE/SIZE/OWN/PROT/WIDTH=(OWN=8,FILENAME=18,SIZE=4)"
$	DDEL*ETE=="DELETE/CONF"
$ !
$ ! SET CORRECT DIRECTORY
$ !
$ LX4:	STDEF
$	SET CONTROL=T
$ !
$ ! DO SUB LOGIN
$ !
$	IF "''WC_LIB'" .NES. "" THEN DELETE/SYMBOL/GLOBAL WC_LIB
$	IF "''WL_LIB'" .NES. "" THEN DELETE/SYMBOL/GLOBAL WL_LIB
$	IF SUBNAME .EQS. "" THEN GOTO LH
$	IF F$SEARCH("''DEFUNUS'LOGIN.COM") .EQS. "" THEN GOTO LH
$	@'DEFUNUS'LOGIN
$ !
$ ! SHOW START
$ !
$ LH:	SHOW DEF
$	EXIT				 !'F$VERIFY(LOCA)'

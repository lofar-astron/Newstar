C+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
C.Ident:	VGEN_INPUT
C.Keywords:	Get Input Line
C.Author:	Ger van Diepen (NFRA, Dwingeloo)
C.Language:	VAX-Fortran
C.Environment:	VAX
C.Comments:
C	Questions from subprocesses are directly asked on SYS$COMMAND.
C	In the future we have to create a special function that can break
C	through read-with-prompts (e.g. the $ of DCL), just like SYS$BRDCST
C	is doing that.
C.Version:	821209 GVD - creation DWC_GETINP
C.Version:	831230 GVD - split into DWC_GETINP and DWC_GETINX
C			- added '#' as an alternative CTRL/Z
C.Version:	840920 JPH - removed all Ferry references, subprocesses now
C			ask via LIB$GET_COMMAND
C			- for subprocesses: prefix CRLF to prompt and broadcast
C			a message 15 sec after calling LIB$GET_COMMAND.
C.Version:	881204 FMO - split into DWC_INPUT and GEN_INPUT
C.Version:	920224 GvD - no optional arguments in MSG anymore
C-------------------------------------------------------------------------
C+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
	INTEGER*4 FUNCTION GEN_INPUT (LINE,PROMPT,DEVCOD)
C
	INCLUDE 'WNG_DEF'
	INCLUDE 'DWC_DEF'
C
	CHARACTER*(*)	LINE		! (o) answer
	CHARACTER*(*)	PROMPT		! (i) prompt
	INTEGER*4	DEVCOD		! (i) input-device code
C
C.Purpose:	Ask a single line from the user
C.Returns:	Status code (.TRUE. for success, .FALSE. otherwise)
C	success	DWC_SUCCESS
C	warning	DWC_EOFCTRLZ	end of file (CTRL/Z or '#' given)
C	info	DWC_GETINPTR	answer truncated
C	fatal	DWC_GETINPERR	I/O error
C.Notes:
C	- If the input device is not a terminal, the prompt argument will be
C	  ignored.
C	- If subprocess: prefix carriage-return and linefeed to the prompt.
C	- An answer of '#' is treated as CTRL/Z.
C-------------------------------------------------------------------------
C
	INCLUDE		'($SSDEF)'
	INCLUDE		'($LIBDEF)'
	INCLUDE		'($RMSDEF)'
C
	CHARACTER*(*)	CRLF
		PARAMETER (CRLF  = CHAR(13)//CHAR(10))
C
	INTEGER*4	MSG_SET  
	INTEGER*4	LIB$GET_INPUT, LIB$GET_COMMAND
	INTEGER*4	SYS$SETIMR, SYS$CANTIM
	EXTERNAL	GEN_INPUT_AST
C
	INTEGER*4	DELTAT(2)
		DATA DELTAT /-150000000,-1/
	CHARACTER*11	DEV(0:2)
		DATA DEV /'SYS$INPUT','SYS$COMMAND','FERRY'/
C
	INTEGER*4	IS
C
C
C					Get a line from the user
C
	IF (DEVCOD.EQ.0) THEN
		IS = LIB$GET_INPUT (LINE,PROMPT)
	ELSE IF (DEVCOD.EQ.1) THEN
		IS = LIB$GET_COMMAND (LINE,PROMPT)
	ELSE
		CALL SYS$SETIMR (,DELTAT,GEN_INPUT_AST,%VAL(DWC_SUBPRCPMT),)
		IS = LIB$GET_COMMAND (LINE,CRLF//PROMPT)
		CALL SYS$CANTIM (%VAL(DWC_SUBPRCPMT),)
	ENDIF
C
C					Check status and return
C
	IF (IS.EQ.RMS$_EOF .OR. IS.EQ.SS$_ENDOFFILE) THEN
		GEN_INPUT = DWC_EOFCTRLZ		! end of input
	ELSE IF (IS.EQ.LIB$_INPSTRTRU) THEN
		GEN_INPUT = DWC_GETINPTR		! answer truncated
	ELSE IF (.NOT.IS) THEN
		IS = MSG_SET (IS,0)			! I/O error
		IS = MSG_SET (DWC_GETINPERR,1)
		CALL WNCTXT(DWLOG,DWMSG,DEV(DEVCOD))
		GEN_INPUT = 4
	ELSE IF (LINE.EQ.'#') THEN
		GEN_INPUT = DWC_EOFCTRLZ		! end of input
	ELSE
		GEN_INPUT = DWC_SUCCESS			! normal completion
	ENDIF
	RETURN
	END
C+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
	SUBROUTINE GEN_INPUT_AST
C
	INCLUDE 'WNG_DEF'
	INCLUDE 'DWC_DEF'
C
C
C.Purpose:	Display the message that the subprocess wants to prompt
C.Returns:	Not applicable
C.Notes:
C-------------------------------------------------------------------------
C
C
	INTEGER*4	MSG_SET
	INTEGER*4	IS
C
	IS = MSG_SET (DWC_SUBPRCPMT,0)
C
	RETURN
	END

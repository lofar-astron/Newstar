C+ NPLDCH.FOR
C  HjV 940112
C       Combined parts of old version of NPLTEL and NPLRES
C
C  Revisions:
C       HjV 940329   Fill in dummy arguments in call to NPLTEL and NPLRES
C	JPH 960729   Comments
C
	SUBROUTINE NPLDCH (LDATTP,IPOL,NHV,IFRS)
C
C  Load data in buffer as (HA, CHAN)
C
C  Result:
C
C	CALL NPLDCH		Load data in buffer as (HA, CHAN)
C
C  Include files:
C
	INCLUDE 'WNG_DEF'
	INCLUDE 'STH_O_DEF'		!SET HEADER
	INCLUDE 'NPL_DEF'
C
C  Parameters:
C
	INTEGER LDATTP			!CURRENT DATA TYPE
	INTEGER IPOL
	INTEGER NHV(0:1)		!# OF PAGES
	INTEGER IFRS(0:1)		!CURRENT IFR'S
C
C  Arguments:
C
C
C  Function references:
C
	LOGICAL NSCSIF			!READ IFR TABLE
	LOGICAL NSCSTL			!GET A SET
C
C  Data declarations:
C
	INTEGER NPLOT			!# OF INTERFEROMETERS TO DO
	INTEGER PTXT(MXNCHN)		!CROSS CHANNELS
	INTEGER SCHAN(0:MXNCHN-1)	!CHANNELS TO DO
	INTEGER*2 IFRT(0:STHIFR-1)	!IFR TABLE
	INTEGER IFRA(0:1,0:STHIFR-1)
	REAL ANG(0:2,0:STHIFR-1)
	REAL BASEL(0:STHIFR-1)		!BASELINES
	INTEGER IRED(0:STHIFR-1)	!REDUNDANT INDICATORS
	INTEGER STHP			!SET HEADER POINTER
	BYTE STH(0:STH__L-1)		!SET HEADER
	  INTEGER*2 STHI(0:STH__L/LB_I-1)
	  INTEGER STHJ(0:STH__L/LB_J-1)
	  REAL STHE(0:STH__L/LB_E-1)
	  DOUBLE PRECISION STHD(0:STH__L/LB_D-1)
	  EQUIVALENCE (STH,STHI,STHJ,STHE,STHD)
C-
C
C FIND REDUNDANT INTERFEROMETERS
C
	CALL WNGMVZ(STH__L,STH(0))			!MAKE SURE PROPER NUMBERS
	IF (NSCSTL(FCAIN,SETS,STH(0),STHP,SETNAM,LPOFF)) THEN !A SET
	  CALL WNDSTR(FCAIN,SETS)			!RESET SET SEARCH
	  IF (OPT.EQ.'RES' .AND. NSRC(0).EQ.0) THEN !REDUNDANCY ASKED
	    IF (.NOT.NSCSIF(FCAIN,STH,IFRT,IFRA,ANG)) THEN	!READ IFR TABLE
	      CALL WNCTXT(F_TP,'Read error IFR table')
	      RETURN
	    END IF
	    CALL NSCMBL(STHE(STH_RTP_E),STHJ(STH_NIFR_J),IFRT,
	1		SIFRS,BASEL)			!MAKE BASELINES
	    CALL NCARRT(STHJ(STH_NIFR_J),BASEL,1E0,IRED,ANG) !GET REDUNDANT
	    DO I1=0,STHJ(STH_NIFR_J)-1			!DELETE NON-REDUNDANT
	      IF (IRED(I1).LE.0) THEN			!NON-REDUNDANT
		SIFRS(IFRA(0,I1),IFRA(1,I1))=.FALSE.
		SIFRS(IFRA(1,I1),IFRA(0,I1))=.FALSE.
	      END IF
	    END DO
	  END IF
	END IF
C
C  FIND CHANNELS TO DO
C
	NPLOT=0						!SELECTED CIFRS
	DO I=0,MXNCHN-1 				!SET NOT CHANNEL
	  SCHAN(I)=-1
	END DO
	DO WHILE(NSCSTL(FCAIN,SETS,STH(0),STHP,SETNAM,LPOFF)) !ALL SETS
	  CALL WNDSTI(FCAIN,SETNAM)			!MAKE SURE OF NAME
	  IF (SETNAM(3).LT.MXNCHN) THEN 		!CAN DO
	    IF (SCHAN(SETNAM(3)).LT.0) THEN		!NEW ONE
	      SCHAN(SETNAM(3))=1			!SET CHANNEL SELECTED
	      NPLOT=NPLOT+1				!COUNT PLOT
	    END IF
	  END IF
	END DO
	I1=0
	DO I=0,MXNCHN-1		        		!SET ORDER
	  IF (SCHAN(I).GT.0) THEN
	    SCHAN(I)=I1	        			!SET ORDER
	    I1=I1+1
	    PTXT(I1)=I  				!SAVE CHANNEL NUMBER
	  END IF
	END DO
C
C PLOT TELESCOPE/RESIDUALS ERRORS
C
 	IF (OPT.EQ.'TEL') THEN                          ! plot telescopes
	   CALL NPLTEL (LDATTP,IPOL,NPLOT,PTXT,NHV,I1,SCHAN,IFRS)
	ELSE			                        ! plot interferometers
	   CALL NPLRES (LDATTP,IPOL,NPLOT,PTXT,NHV,I1,SCHAN,IFRS)
	ENDIF
C
C
	END


C+ NCARGR.FOR
C  WNB 910128
C
C  Revisions:
C	WNB 910813	Selection of baselines
C	JPH 960520	Left and right plot annotations: F7.0 --> E7.0 to
C			 prevent overflow
C
	SUBROUTINE NCARGR(HEAD,IFTAB,IFR,ADAT,PDAT,NIFR,AP)
C
C  Print graphical ampl/phase data
C
C  Result:
C
C	CALL NCARGR( HEAD_C*:I, IFTAB_J(0:NIFR-1):I, IFR_I(0:*):I,
C				ADAT_E(0:NIFR-1):I, PDAT_E(0:NIFR-1):I,
C				NIFR_J:I, AP_B(0:1):I)
C
C				Print a graph of Ampl/Phase data given in
C				DAT, for NIFR interferometers, selecting
C				the ones pointed to in IFTAB.
C				HEAD specifies a title, AP indicates if ampl
C				and/or phase(.TRUE.) are present.
C
C  Include files:
C
	INCLUDE 'WNG_DEF'
	INCLUDE 'STH_O_DEF'
C
C  Parameters:
C
C
C  Arguments:
C
	CHARACTER*(*) HEAD			!TITLE OF GRAPH
	INTEGER IFTAB(0:*)			!SELECTION TABLE
	INTEGER*2 IFR(0:*)			!INTERFEROMETER NAMES
	REAL ADAT(0:*),PDAT(0:*)		!AMPL/PHASE DATA
	INTEGER NIFR				!# OF INTERFEROMETERS TO DO
	BYTE AP(0:1)				!AMPL/PHASE INDICATOR
        LOGICAL AP0
        LOGICAL AP1
        CHARACTER*80 ARGSTR
C
C  Function references:
C
C
C  Data declarations:
C
C-
	REAL GMX,GMN,PMX,PMN			!MAX/MIN VALUES A/P
	REAL PMD,GMD				!MIDDLE VALUES
	REAL PD,GD				!INTERVALS
	CHARACTER*(STHIFR+6) TXT		!TEXT LINE
	CHARACTER*36 TELC			!TELESCOPE NAMES
	  DATA TELC/'0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZ'/
C
C INIT
C
	IF (NIFR.LE.0) RETURN				!NO DATA
C
	PMX=-1E6					!INIT. VALUES
	PMN=1E6
	GMX=-1E6
	GMN=1E6
	DO I=0,NIFR-1					!FIND MAX/MIN
	  GMX=MAX(GMX,ADAT(I))				!AMPL
	  GMN=MIN(GMN,ADAT(I))
	  PMX=MAX(PMX,PDAT(I))				!PHASE
	  PMN=MIN(PMN,PDAT(I))
	END DO
C
	GMD=(GMX+GMN)/2					!MIDDLE VALUES
	PMD=(PMX+PMN)/2
	GD=MAX(1E-6,(GMX-GMN)/12)			!STEP
	PD=MAX(1E-6,(PMX-PMN)/12)
C
	I1=MIN(NIFR+5,LEN(HEAD))			!ARANGE HEADER LINE
	I2=(NIFR+6-I1)/2
	TXT=' '
        ARGSTR='   (A)'//TXT(:I2)//HEAD(:I1)//TXT(:NIFR+6-I1-I2)//'(P)'
	CALL WNCTXT(F_P,ARGSTR)
C
C PRINT GRAPH
C
	DO I=6,-6,-1
	  TXT=' '
	  DO I1=0,NIFR-1
	    AP0=AP(0)
            AP1=AP(1)
	    IF (NINT((ADAT(I1)-GMD)/GD).EQ.I .AND. AP0)
	1		TXT(I1+1:I1+1)='A'
	    IF (NINT((PDAT(I1)-PMD)/PD).EQ.I .AND. AP1) THEN
	      IF (TXT(I1+1:I1+1).EQ.'A') THEN
		TXT(I1+1:I1+1)='X'
	      ELSE
		TXT(I1+1:I1+1)='P'
	      END IF
	    END IF
	  END DO
	  CALL WNCTXT(F_P,'!7$E7.0 '//TXT(:NIFR)//
	1		' !7$E7.0',GMD+I*GD,PMD+I*PD)
	END DO
C
C PRINT BOTTOM LINES
C
	TXT=' '
	DO I=0,NIFR-1
	  TXT(I+1:I+1)=TELC(MOD(IFR(IFTAB(I)),256)+1:) !TELESCOPE NAME
	END DO
	CALL WNCTXT(F_P,'!9C!AS',TXT(:NIFR))
	TXT=' '
	DO I=0,NIFR-1
	  TXT(I+1:I+1)=TELC(IFR(IFTAB(I))/256+1:) !TELESCOPE NAME
	END DO
	CALL WNCTXT(F_P,'!9C!AS!/',TXT(:NIFR))
C
	RETURN
C
C
	END

















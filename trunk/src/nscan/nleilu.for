C+ NLEILU.FOR
C  HjV 951101
C
C  Revisions:
C       WNB 951212      Correct MAX(real,integer) routine
C       HjV 960111	Change CONTLINE in OBSMODE
C
C
	SUBROUTINE NLEILU(TYP,VOLUME,LABEL,FVERS,
	1		OBSDATE,OBSSTART,OBSEND,OLSYS,PRNAME,FIELD,
	1		NBL,BYPBL,FBNDS,FBNDE,DIPOLE,
	1		STH,STHI,STHJ,STHE,STHD)
C
C  List header and optionally update the archive
C
C  Result:
C
C	CALL NLEILU(TYP_J:I, VOLUME_C(6)I, LABEL_J:I, FVERS_I:I,
C			OBSDATE_J:I, OBSSTART_D:I, OBSEND_D:I, 
C			OLSYS_I:I, PRNAME_C(3):I, FIELD_C(16):I,
C			FBNDS_I:I, FBNDE_I:I, DIPOLE_I:I,
C			NBL_J:I, BYPBL_I:I, ...)	
C				List Leiden data from headers if TYP is 1, 
C				or list and update Scissor if TYP is 2.
C				VOLUME is the name of the volume or blank
C				LABEL is the label on the input tape
C				FVERS is the tape-format version
C				OBSDATE is Observation date (yyddd)
C				OBSSTART is Observation start-time in sec.
C				OBSEND is Observation end-time in sec.
C				OLSYS is online computer program nr.
C				PRNAME is initials of project scientist
C				FIELD is fieldname
C				NBL is number of blocks within the dataset
C				BYPBL is number of bytes per block
C				FBNDS is start freq.-band
C				FBNDE is end freq.-band
C				DIPOLE is the dipole position
C
C  Include files:
C
	INCLUDE 'WNG_DEF'
	INCLUDE 'NSC_DEF'			!FOR OPTION AND SRTRCL
	INCLUDE 'STH_O_DEF'			!SET HEADER
C
C  Arguments:
C
	INTEGER TYP				! 1:LIST 2:LIST AND ARCHIVE
	CHARACTER*6 VOLUME			!VOLUME LABEL IF ANY
	INTEGER LABEL				!LABEL NUMBER
	INTEGER*2 FVERS				!Tape-format version
	INTEGER OBSDATE				!Obs. date (yyddd)
	REAL*8  OBSSTART			!Obs. start-time in SEC.
	REAL*8  OBSEND				!Obs. end-time in SEC.
	INTEGER*2 OLSYS				!Online program nr.
	CHARACTER*3 PRNAME			!Initals project scientist
	CHARACTER*16 FIELD			!Field name
	INTEGER NBL				!# block within dataset
	INTEGER*2 BYPBL				!# bytes per block
	INTEGER*2 FBNDS,FBNDE			!start/end freq.band
	INTEGER*2 DIPOLE			!DIPOLE number
C
	BYTE STH(0:STHHDL-1)			!SET HEADER
	  INTEGER*2 STHI(0:STHHDL/2-1)
	  INTEGER   STHJ(0:STHHDL/4-1)
	  REAL STHE(0:STHHDL/4-1)
	  REAL*8 STHD(0:STHHDL/8-1)
C
C  Function references:
C
	LOGICAL WNFRD				!READ FROM FCA
	LOGICAL WNFOPF				!OPEN LABEL
	INTEGER WNCALN				!LENGTH OF STRING
	INTEGER WNFSCI				!CALL DATABASE
C
C  Data declarations:
C
	INTEGER*2 NULL
	   DATA NULL/0/
	INTEGER*2 ONLINE			!ONLINE SYSTEMNR.
	INTEGER DURATION			!DURATION OF OBS. IN SEC.
	INTEGER UTD,UTM,UTY			!UT START-TIME DAY,MONTH,YEAR
	INTEGER UTDAY
	DOUBLE PRECISION RA1,DEC1		!RA AND DEC OF OBS.
	REAL POS9A,POS9B,POS9C,POS9D		!POSITION TEL. 9
	REAL*8  UTSTART,UTEND			!UT-time
	CHARACTER*2 DIPC			!DIPOLE SETTING
	CHARACTER*4 BECODE			!BACKEND NAME
	CHARACTER*32 POLC			!POLARISATIONS
	CHARACTER*9 OBSMODE			!OBSERVATION MODE
	CHARACTER*32 ARUSE			!ARRAY USE
	BYTE RWBUF(SRTRCL)			!I/O BUFFER FOR CHECK
	INTEGER*2 OBSHR,OBSMIN			!OBS.-TIME
	INTEGER OBSY,OBSM,OBSD			!OBS.-TIME
	CHARACTER*12 SOURCE
	INTEGER PROJECT				!PROJECTNR.
        INTEGER LTXT                            !LENGTH STRING TO USE
        INTEGER PTXT                            !STRING POINTER
	CHARACTER*1024 WARC_OBS			!BUFFER FOR ARCHIVE
	CHARACTER*1024 WARC			!BUFFER FOR ARCHIVE
	INTEGER WAVEL				!WAVELENGTH IN CM

C
	INTEGER DAYS(12)			!DAYS PER MONTH
	   DATA DAYS/31,28,31,30,31,30,31,31,30,31,30,31/

        CHARACTER*10 CDIG                       !DIGITS
                DATA CDIG/'0123456789'/

	INTEGER LEIRCL				! MOET IN FDL.DSC
	   DATA LEIRCL/3840/
C
C
	DURATION=(STHE(STH_HAI_E)*STHJ(STH_SCN_J))*DCRTSC
	OBSHR=INT(OBSSTART/3600.)
	OBSMIN=INT((OBSSTART-(OBSHR*3600.))/60.)
C EXTRACT SOME INFO FROM THE FIELDNAME
	PROJECT=5
	OBSMODE=' '
	IF (FIELD(1:1).EQ.'W') THEN			!WESTERBORK OBS.
	   LTXT=INDEX(FIELD,'.')
	   IF (LTXT.EQ.0) LTXT=INDEX(FIELD,'<')
	   IF (LTXT.EQ.0) LTXT=INDEX(FIELD,',')
	   IF (LTXT.GT.0) THEN				!OK, A DOT, COMMA or < IN FIELDNAME
	      IF (FIELD(1:1).EQ.'C') THEN		!21cm cont
		 OBSMODE='Continuum'
	      ELSE IF (FIELD(1:1).EQ.'H') THEN		!21cm line
		 OBSMODE='Line'
	      ELSE IF (FIELD(1:1).EQ.'R') THEN		!6cm line
		 OBSMODE='Line'
	      ELSE IF (FIELD(1:1).EQ.'S') THEN		!6cm cont
		 OBSMODE='Continuum'
	      ELSE IF (FIELD(1:1).EQ.'T') THEN		!50cm cont
		 OBSMODE='Continuum'
	      END IF
	      PROJECT=0					!RESULT
	      PTXT=0
	      DO WHILE (PTXT+1.LE.LTXT)
		 J2=INDEX(CDIG,FIELD(PTXT+1:PTXT+1))
		 IF (J2.GT.0) THEN
		    PROJECT=PROJECT*10+J2-1		!ADD DIGIT
		 END IF
		 PTXT=PTXT+1				!NEXT DIGIT
	      END DO
	      SOURCE=FIELD(LTXT+1:LEN(FIELD))
	   ELSE						!NO DOT IN FIELDNAME
	      SOURCE=FIELD(1:LEN(FIELD))
	   ENDIF
	ELSE						!NOT WESTERBORK OBS.
	   SOURCE=FIELD(1:LEN(FIELD))
	END IF
C
C Oplossen probleemgevallen
	IF (FIELD(1:6).EQ.'WH.CYG') PROJECT=13
	IF (FIELD(1:5).EQ.'WS85,') THEN
	    PROJECT=85  
	    SOURCE=FIELD(6:LEN(FIELD))
	    OBSMODE='Continuum'
	END IF
	IF (FIELD(1:4).EQ.'WS86') THEN
	    PROJECT=86
	    SOURCE=FIELD(5:LEN(FIELD))
	    OBSMODE='Continuum'
	END IF
	IF (FIELD(1:4).EQ.'WHX.') THEN
	    PROJECT=5  
	    SOURCE=FIELD(5:LEN(FIELD))
	    OBSMODE='Line'
	END IF
	IF (FIELD(1:4).EQ.'WX6\') THEN
	    PROJECT=6
	    SOURCE=FIELD(5:LEN(FIELD))
	END IF
	IF (FIELD(1:3).EQ.'W5\') THEN
	    PROJECT=5
	    SOURCE=FIELD(4:LEN(FIELD))
	END IF
	IF (FIELD(1:4).EQ.'W13\') THEN
	    PROJECT=13
	    SOURCE=FIELD(5:LEN(FIELD))
	END IF
	IF (FIELD(1:4).EQ.'W31\') THEN
	    PROJECT=31
	    SOURCE=FIELD(5:LEN(FIELD))
	END IF
	IF (FIELD(1:5).EQ.'W100\') THEN
	    PROJECT=100
	    SOURCE=FIELD(6:LEN(FIELD))
	END IF
	IF (FIELD(1:5).EQ.'W101\') THEN
	    PROJECT=101
	    SOURCE=FIELD(6:LEN(FIELD))
	END IF
	IF (FIELD(1:5).EQ.'W7CYG') THEN
	    PROJECT=7
	    SOURCE=FIELD(3:LEN(FIELD))
	END IF
	IF (FIELD(1:5).EQ.'W4HER') THEN
	    PROJECT=4
	    SOURCE=FIELD(3:LEN(FIELD))
	END IF
	IF (FIELD(1:4).EQ.'W5 N') THEN
	    PROJECT=5
	    SOURCE=FIELD(4:LEN(FIELD))
	END IF
	IF (FIELD(1:4).EQ.'W16 ') THEN
	    PROJECT=16
	    SOURCE=FIELD(5:LEN(FIELD))
	END IF
	IF (FIELD(1:4).EQ.'W17 ') THEN
	    PROJECT=17
	    SOURCE=FIELD(5:LEN(FIELD))
	END IF
	IF (FIELD(1:4).EQ.'W69 ') THEN
	    PROJECT=69
	    SOURCE=FIELD(5:LEN(FIELD))
	END IF
	IF (FIELD(1:5).EQ.'W204C') THEN
	    PROJECT=20  
	    SOURCE=FIELD(4:LEN(FIELD))
	END IF
	IF (FIELD(1:5).EQ.'W20 4') THEN
	    PROJECT=20  
	    SOURCE=FIELD(5:LEN(FIELD))
	END IF
	IF (FIELD(1:5).EQ.'WH14N') THEN
	    PROJECT=14  
	    SOURCE=FIELD(5:LEN(FIELD))
	    OBSMODE='Line'
	END IF
	IF (FIELD(1:8).EQ.'WH161NGC') THEN
	    PROJECT=161  
	    SOURCE=FIELD(6:LEN(FIELD))
	END IF
	IF (FIELD(1:5).EQ.'WS124') THEN
	    PROJECT=124
	    SOURCE=FIELD(6:LEN(FIELD))
	END IF
	IF (FIELD(1:4).EQ.'WS11') THEN
	    PROJECT=11
	    SOURCE=FIELD(5:LEN(FIELD))
	END IF
	IF (FIELD(1:6).EQ.'WS155_') THEN
	    PROJECT=155
	    SOURCE=FIELD(7:LEN(FIELD))
	    OBSMODE='Continuum'
	END IF
	IF (FIELD(1:4).EQ.'WS3C') THEN
	    PROJECT=5
	    SOURCE=FIELD(3:LEN(FIELD))
	    OBSMODE='Continuum'
	END IF
	IF (FIELD(1:6).EQ.'WC182P') THEN
	    PROJECT=182
	    SOURCE=FIELD(6:LEN(FIELD))
	    OBSMODE='Continuum'
	END IF
C
C
	CALL WNCTXT(F_TP,
	1	   '!4$UJ: !9$SJ !-12$AS !4$UJ   !2$ZJ!2$ZJ   '//
	1	   '!2$ZI!3$ZI !2$ZI!2$ZI    '//
	1	   '!10$DPF15.5 !10$DAF15.5',
	1		LABEL,STHJ(STH_VNR_J),
	1		SOURCE,PROJECT,
	1		(DURATION/3600),MOD(DURATION/60,60),
	1		STHI(STH_OBS_I+1),STHI(STH_OBS_I),
	1		OBSHR,OBSMIN,
	1		STHD(STH_RA_D),STHD(STH_DEC_D))
C
C  TYP will be 2 if the WARC option had been choosen.
C  There are two suboptions now: CHECK or ARCHIVE.
C
C  With CHECK we scan the whole observation on tape to catch parity 
C  errors, with ARCHIVE the Observation, Obspos and Mediad views in Scissor
C  are informed of the label through WNFSCI.
C
	IF (TYP.EQ.2.AND.OPTION(4:4).EQ.'C') THEN	!Read all blocks
	   J=NBL					!Number of blocks
	   J1=BYPBL					!Blocksize
	   IF (J1.NE.LEIRCL) THEN
	      CALL WNCTXT(F_TP,'Invalid blocksize for label !UJ',LABEL)
	   END IF
C
C  For tapes, reopen (some units are slow in rewinding)
C
	   IF (UNIT.EQ.'D') THEN			!No check for Disks
	   ELSE IF (.NOT.WNFOPF(IMCA,' ','R',0,0,0,LABEL)) THEN
	      CALL WNCTXT(F_TP,'Cannot re-open label !UJ',LABEL)
	   ELSE
	      I1=0					!No bad records yet
	      DO J2=0,J-1
	        IF (.NOT.WNFRD(IMCA,SRTRCL,RWBUF,J2*J1)) THEN	!Error
	          I1=I1+1
	          IF (I1.LT.3) THEN
		    CALL WNCTXT(F_TP,
	1	      'Cannot read record !UJ for label !UJ',J2,LABEL)
	          END IF
	        END IF
	      END DO
	      IF (I1.GE.3) THEN
	         CALL WNCTXT(F_TP,
	1	   'Cannot read !UJ records for label !UJ',I1,LABEL) 
	      ELSE IF (I1.EQ.0) THEN
	         CALL WNCTXT(F_TP,
	1	   'Total of !UJ records checked for label !UJ',J,LABEL)
	      END IF
	   END IF
C
	ELSE IF (TYP.EQ.2.AND.OPTION(4:4).EQ.'A') THEN	!Inform Scissor
C
	   IF (OBSDATE.LT.77000) THEN
	      BECODE='ANA'			!BE CODE
	   ELSE 
	      IF (PRNAME.EQ.'DLB' .OR. PRNAME.EQ.'DCC') THEN
		 BECODE='DLB'			!BE CODE
	      ELSE
		 BECODE='ANA'			!BE CODE
	      END IF
	   END IF
C
	   RA1=STHD(STH_RA_D)*360.D0
	   IF (RA1.LT.0) RA1=RA1+360.D0		!BETWEEN 0..360
	   DEC1=STHD(STH_DEC_D)*360.D0
C
	   IF (OBSMODE.EQ.' ') THEN
	      OBSMODE='Continuum'
	      IF ((STHE(STH_RTP_E)  .EQ.STHE(STH_RTP_E+5)) .AND.
	1	  (STHE(STH_RTP_E+1).EQ.STHE(STH_RTP_E+6)) .AND.
	1	  (STHE(STH_RTP_E+2).EQ.STHE(STH_RTP_E+7)) .AND.
	1	  (STHE(STH_RTP_E+3).EQ.STHE(STH_RTP_E+8)) .AND.
	1	  (STHE(STH_RTP_E+4).EQ.STHE(STH_RTP_E+9))) OBSMODE='Line'
	   END IF
C
	   IF (MOD(INT(DIPOLE/4),2).EQ.0) THEN
	      DIPC(1:1)='+'
	   ELSE 
	      DIPC(1:1)='X'
	   END IF
	   IF (MOD(DIPOLE,2).EQ.0) THEN
	      DIPC(2:2)='+'
	   ELSE 
	      DIPC(2:2)='X'
	   END IF
C
C Some observations have obs-date: x-y-1936 (yes, 1936)
C These observations should have year 1973, so I will change it here
	   OBSY=INT(OBSDATE/1000)			!YEAR
	   OBSD=OBSDATE-(OBSY*1000)			!DAYNR
	   OBSY=OBSY+1900
	   IF (OBSY.EQ.1936) OBSY=1973
	   CALL WNGU2S (-1,OBSY,UTDAY,UTSTART,OBSD,OBSSTART/86400.)
	   CALL WNGU2S (-1,OBSY,UTDAY,UTEND,OBSD,OBSEND/86400.)
C
	   OBSY=INT(OBSDATE/1000)			!YEAR
	   OBSD=OBSDATE-(OBSY*1000)			!DAYNR
	   OBSY=OBSY+1900
	   IF (OBSY.EQ.1936) OBSY=1973
	   OBSM=1
	   IF (MOD(OBSY,4).EQ.0) DAYS(2)=DAYS(2)+1
	   DO WHILE (OBSD.GT.DAYS(OBSM))
	     OBSD=OBSD-DAYS(OBSM)
	     OBSM=OBSM+1
	   END DO
	   IF (MOD(OBSY,4).EQ.0) DAYS(2)=DAYS(2)-1
	   IF (OBSM.GT.12) THEN
	      OBSM=OBSM-12
	      OBSY=OBSY+1
	   END IF
	   IF (OBSD.EQ.0) OBSD=1
C
	   UTY=1900+STHI(STH_OBS_I+1)
	   UTD=STHI(STH_OBS_I)
	   UTM=1
	   IF (UTY.EQ.1936) UTY=1973
	   IF (MOD(STHI(STH_OBS_I+1),4).EQ.0) DAYS(2)=DAYS(2)+1
	   DO WHILE (UTD.GT.DAYS(UTM))
	     UTD=UTD-DAYS(UTM)
	     UTM=UTM+1
	   END DO
	   IF (MOD(STHI(STH_OBS_I+1),4).EQ.0) DAYS(2)=DAYS(2)-1
	   IF (UTM.GT.12) THEN
	      UTM=UTM-12
	      UTY=UTY+1
	   END IF
	   IF (UTD.EQ.0) UTD=1
C
	   IF (STHJ(STH_NIFR_J).EQ.80) THEN
	      ARUSE='X0123456789AB.._Y0123456789AB.. '
	   ELSE
	      ARUSE='X0123456789ABCD_Y0123456789ABCD '
	   ENDIF
C
	   IF (STHD(STH_FRQ_D).GE.200 .AND. STHD(STH_FRQ_D).LE.400) THEN
	      WAVEL=92
	   ELSE IF (STHD(STH_FRQ_D).GE.500 .AND. STHD(STH_FRQ_D).LE.800) THEN
	      WAVEL=50
	   ELSE IF (STHD(STH_FRQ_D).GE.1200 .AND. STHD(STH_FRQ_D).LE.1500) THEN
	      WAVEL=21
	   ELSE IF (STHD(STH_FRQ_D).GE.1600 .AND. STHD(STH_FRQ_D).LE.1800) THEN
	      WAVEL=18
	   ELSE IF (STHD(STH_FRQ_D).GE.4600 .AND. STHD(STH_FRQ_D).LE.5100) THEN
	      WAVEL=6
	   ELSE
	      WAVEL=0
	   END IF
C
C Zet Online-version op 0
C Het aanpassen van WSRTOBS file definition stuit op system probleem.
	   OLSYS=0
C
C
	   CALL WNCTXS(WARC_OBS,
	1	'PUT=OBSERVATION '//
	1	'SEQNUMBER=!SJ PROJECT=!UJ '//
	1	'EPOCHE=!E SOURCE=!AS',
	1	STHJ(STH_VNR_J),PROJECT,
	1	STHE(STH_EPO_E),SOURCE)
C
	   CALL WNCTXS(WARC_OBS(WNCALN(WARC_OBS)+1:),
	1	' SETS_START=!UI SETS_END=!UI '//
	1	'TAPE_VERSION=!SI ONLINE_VERSION=!SI '//
	1	'OBS_DATE=!2$ZJ/!2$ZJ/!4$ZJ OBS_TIME=!UJ '//
	1	'UT_DATE=!2$ZJ/!2$ZJ/!4$ZJ '//
	1	'UT_START=!UJ UT_END=!UJ '//
	1	'HA_START=!D HA_END=!D ',
	1	FBNDS,FBNDE,
	1	FVERS,OLSYS,
	1	OBSD,OBSM,OBSY,INT(OBSSTART),
	1	UTD,UTM,UTY,
	1	INT(86400*UTSTART),INT(86400*UTEND),
	1	STHE(STH_HAB_E)*360.D0,
	1	(STHE(STH_HAB_E)+STHE(STH_HAI_E)*STHJ(STH_SCN_J))*360.D0)
C
	   CALL WNCTXS(WARC_OBS(WNCALN(WARC_OBS)+1:),
	1	' NR_SETS=!UI NR_POLARISATION=!UI '//
	1	'NR_INTERFEROM=!UJ '//
	1	'NR_FREQ=!UI NR_FEQ=!UI NR_CORRCHAN=!UI '//
	1	'POSA_9=!E POSB_9=!E POSC_9=!E POSD_9=!E '//
	1	'ARRAY_USE=!AS ',
	1	FBNDE-FBNDS+1,STHI(STH_PLN_I),
	1	STHJ(STH_NIFR_J),NULL,
	1	NULL,NULL,
	1	STHE(STH_RTP_E+10)-STHE(STH_RTP_E+9),
	1	STHE(STH_RTP_E+11)-STHE(STH_RTP_E+9),
	1	MAX(0.,STHE(STH_RTP_E+12)-STHE(STH_RTP_E+9)),
	1	MAX(0.,STHE(STH_RTP_E+13)-STHE(STH_RTP_E+9)),
	1	ARUSE)
C
	   CALL WNCTXS(WARC_OBS(WNCALN(WARC_OBS)+1:),
	1	' FREQUENCY=!D '//
	1	'BANDWIDTH=!E '//
	1	'BACKEND.DESCRIPTION=!AS '//
	1	'OBSMODE.DESCRIPTION=!AS '//
	1	'DIPOLE.SYMBOLIC=!AS ',
	1	STHD(STH_FRQ_D),
	1	STHE(STH_BAND_E),
	1	BECODE,OBSMODE,DIPC)
C
C Create entry in PROJECTOR if project not exist
	   CALL WNCTXS(WARC,'SELECT=PROJECTOR PROJECT=!UJ',PROJECT)
	   J0=WNFSCI(WARC)
	   IF (J0.LT.0) THEN
	      CALL WNFSCS(WARC)
	      CALL WNCTXT(F_TP,'Error !SJ , will NOT continue this label',
	1	J0)
	      RETURN
	   END IF
	   IF (MOD(J0,100).NE.0) THEN			!PROJECT DOES NOT EXIST
	      CALL WNFSCS(WARC)
	      CALL WNCTXT(F_TP,'Creating project !UJ in PROJECTOR',project)
	      CALL WNCTXS(WARC,
	1	   'PUT=PROJECTOR PROJECT=!UJ PROPOSALNR=!UJ '//
	1	   'SUBJECT=Observations from LEIDEN archive '//
	1	   'CATEGORY=Unknown WAVELENGTH=!UJ ALIAS=Leiden data',
	1	   PROJECT,PROJECT,WAVEL)
	      J0=WNFSCI(WARC)
	      IF (MOD(J0,100).NE.0) THEN
		 CALL WNFSCS(WARC)
		 CALL WNCTXT(F_TP,'Error !UJ updating archive: !AS',
	1	      J0,WARC(1:WNCALN(WARC)))
	      END IF
	   END IF
C
C Update OBSERVATION
	   J0=WNFSCI(WARC_OBS)
	   IF (MOD(J0,100).NE.0) THEN
	      CALL WNFSCS(WARC_OBS)
	      CALL WNCTXT(F_TP,'Error !SJ updating archive: !AS',
	1	   J0,WARC_OBS(1:WNCALN(WARC_OBS)))
	   END IF
C
C Update MEDIAD
	   CALL WNCTXS(WARC,
	1	'PUT=MEDIAD VOLUME=!AS LABEL=!UJ SEQNUMBER=!SJ PROJECT=!UJ',
	1	VOLUME,LABEL,STHJ(STH_VNR_J),PROJECT)
	   J0=WNFSCI(WARC)
	   IF (MOD(J0,100).NE.0) THEN
	      CALL WNFSCS(WARC)
	      CALL WNCTXT(F_TP,'Error !SJ updating archive: !AS',
	1	   J0,WARC(1:WNCALN(WARC)))
	   END IF
C
C Update OBSPOS
	   CALL WNCTXS(WARC,
	1	'PUT=OBSPOS SEQNUMBER=!SJ '//
	1	'RA=!D DEC=!D EQUINOX=B1950 SYSTEM=FK5 OH_NO=0',
	1	STHJ(STH_VNR_J),RA1,DEC1)
	   J0=WNFSCI(WARC)
	   IF (MOD(J0,100).NE.0) THEN
	      CALL WNFSCS(WARC)
	      CALL WNCTXT(F_TP,'Error !SJ updating archive: !AS',
	1	   J0,WARC(1:WNCALN(WARC)))
	   END IF
	END IF
C
	RETURN
	END

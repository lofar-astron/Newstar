C+ NSCSWI.FOR
C  WNB 940810
C
C  Revisions:
C       WNB 950628	Read IFR corrections only once
C       WNB 950704	Add AIFR pre-correction
C
	LOGICAL FUNCTION NSCSWI(FCA,STHJ,SCN,IFRT,COR,TCOR,
	1				CAP,CDAP,ZAP)
C
C  Write interferometer correction data
C
C  Result:
C
C	NSCSWI_L = NSCSWI( FCA_J:I, STHJ_B(0:*):I, SCN_J:I, IFRT_I(*):I,
C			COR_X(0:3,0:*):I, TCOR_J:I,
C			CAP_J:I, CDAP_J:I, ZAP_J:I)
C	The file FCA with set header STH and scan number SCN will have 
C corrections submitted in COR added to its SCH table indicated by the mask
C TCOR, - whose value may be CAP_AIFR or _MIFR.
C	If the TCOR bit in ZAP is set, the corrections will be zeroed
C unconditionally.
C	The bits in CAP/CDAP indicate which corrections were used in
C determining the COR, and will be used to 'de-correct' the input COR.
C	For TCOR CAP_AIFR the COR are COS/SIN; for _MIFR (log(gain),phase)
C
C  Include files:
C
	INCLUDE 'WNG_DEF'
	INCLUDE 'CBITS_DEF'
	INCLUDE 'STH_O_DEF'			!SET HEADER
	INCLUDE 'SCH_O_DEF'			!SCAN HEADER
C
C  Parameters:
C
C
C  Arguments:
C
	INTEGER FCA			!FILE CONTROL AREA
	INTEGER STHJ(0:*)		!CURRENT SET HEADER
	INTEGER SCN			!SCAN # TO DO
	INTEGER*2 IFRT(0:*)		!INTERFEROMETER TABLE
	COMPLEX COR(0:3,0:*)		!CORRECTIONS
	INTEGER TCOR			!TYPE CORRECTION
	INTEGER CAP			!APPLIED CORRECTIONS
	INTEGER CDAP			!DE-APPLIED CORRECTIONS
	INTEGER ZAP			!ZERO CORRECTIONS
C
C  Function references:
C
	LOGICAL WNFRD			!READ DATA
	LOGICAL WNFWR			!WRITE DATA
	INTEGER WNFEOF			!GET END OF FILE
	LOGICAL NSCSCY			!READ EXTENDED CORRECTIONS
C
C  Data declarations:
C
	LOGICAL DOMIFR			!SWAP MIFR/AIFR
	INTEGER POFF			!POINTER MIFR/AIFR OFFSET
	INTEGER SCNP			!CURRENT SCAN DISK PTR
	COMPLEX LCOR(0:3,0:STHIFR-1)	!LOCAL CORRECTION BLOCK
	COMPLEX TLCOR(0:STHTEL-1,0:1)	!TEL. CORRECTIONS
	COMPLEX IMCOR(0:3,0:STHIFR-1,0:1) !(DE-)APPLY MIFR CORR
	COMPLEX IACOR(0:3,0:STHIFR-1,0:1) !(DE-)APPLY AIFR CORR
	REAL FACOR(2,2)			!FARADAY ROTATION
	COMPLEX PLCOR(0:STHTEL-1,0:1)	!POL. CORRECTION
	COMPLEX CI
	INTEGER CPLC(0:3,0:1)		!X,Y IDENTIFIERS
	  DATA CPLC/0,0,1,1,0,1,0,1/
	BYTE SCH(0:SCH__L-1)		!SCAN HEADER
	  INTEGER SCHJ(0:SCH__L/LB_J-1)
	  REAL SCHE(0:SCH__L/LB_E-1)
	  EQUIVALENCE (SCH,SCHJ,SCHE)
C-
C
C INIT
C
	NSCSWI=.TRUE.				!ASSUME OK
	IF (IAND(TCOR,CAP_MIFR).NE.0) THEN	!MIFR
	  DOMIFR=.TRUE.
	  POFF=SCH_IFRMC_J			!HEADER OFFSET
	ELSE IF (IAND(TCOR,CAP_AIFR).NE.0) THEN	!AIFR
	  DOMIFR=.FALSE.
	  POFF=SCH_IFRAC_J
	ELSE
C
	  RETURN				!NOTHING TO DO
	END IF
	CALL WNGMVZ(4*LB_X*STHIFR,LCOR)		!ASSUME NO CORRECTIONS
C
C READ SCAN HEADER AND CORRECTIONS
C
	IF (.NOT.NSCSCY(FCA,STHJ,IFRT,SCN,CAP,CDAP,SCH,TLCOR,
	1		IMCOR,FACOR,PLCOR,IACOR)) GOTO 900 !SCAN/CORRECTIONS
	SCNP=STHJ(STH_SCNP_J)+SCN*STHJ(STH_SCNL_J) !GET SCN HEADER POINTER
C
C ZERO CORRECTIONS
C
	IF (IAND(TCOR,ZAP).NE.0) THEN		!ZERO ASKED
	  IF (SCHJ(POFF).NE.0) GOTO 100		!CORRECTIONS PRESENT: REWRITE
	  GOTO 800				!READY
	END IF
C
C ADD TO OLD
C
	IF (DOMIFR) THEN
	   IF (IAND(TCOR,CAP).NE.0) THEN 	!OLD WAS APPLIED
	      CALL WNGMV(4*LB_X*STHJ(STH_NIFR_J),IMCOR(0,0,0),LCOR(0,0)) !GET
	   END IF
	ELSE
	   IF (IAND(TCOR,CAP).NE.0) THEN 	!OLD WAS APPLIED
	      CALL WNGMV(4*LB_X*STHJ(STH_NIFR_J),IACOR(0,0,0),LCOR(0,0)) !GET
	   END IF
	END IF
C
C DETERMINE CORRECTIONS
C
C APPLY OLD CORRECTIONS
C
	IF (.NOT.DOMIFR) THEN			!RE-CORRECT AIFR
C
C NOTE: FARADAY ROTATION AND POLARISATION CORRECTIONS NOT IMPLEMENTED
C       CORRECTLY. HENCE ITERATION NECESSARY.
C
	   IF (IAND(CDAP,CAP_AIF).NE.0) THEN 	!WANTED AND PRESENT
	      DO I=0,STHJ(STH_NIFR_J)-1		!ALL IFRS
		 DO I1=0,3			!ALL POL.
		    COR(I1,I)=COR(I1,I)-IACOR(I1,I,1)
		 END DO
	      END DO
	   END IF
	   IF (IAND(CAP,CAP_MIF).NE.0) THEN 	!WANTED AND PRESENT
	      DO I=0,3				!ALL POL.
		 DO I3=0,STHJ(STH_NIFR_J)-1	!ALL IFRS
		    CI=IMCOR(I,I3,0) 		!IFR APPLY
		    COR(I,I3)=COR(I,I3)*EXP(+CI) !CORRECT DATA POINT
		 END DO				!IFRS
	      END DO				!POL.
	   END IF
	   IF (IAND(CDAP,CAP_MIF).NE.0) THEN 	!WANTED AND PRESENT
	      DO I=0,3				!ALL POL.
		 DO I3=0,STHJ(STH_NIFR_J)-1 	!ALL IFRS
		    CI=-IMCOR(I,I3,1) 		!IFR DE-APPLY
		    COR(I,I3)=COR(I,I3)*EXP(+CI) !CORRECT DATA POINT
		 END DO				!IFRS
	      END DO				!POL.
	   END IF
	   IF (IAND(IOR(CAP,CDAP),CAP_TELMSK).NE.0) THEN !WANTED
	      DO I=0,3				!ALL POL.
		 I1=CPLC(I,0)			!X ID
		 I2=CPLC(I,1)			!Y ID
		 DO I3=0,STHJ(STH_NIFR_J)-1	!ALL IFRS
		    CI=TLCOR(MOD(IFRT(I3),256),I1)+
	1		 CONJG(TLCOR(IFRT(I3)/256,I2)) !CORRECTION
		    COR(I,I3)=COR(I,I3)*EXP(+CI) !CORRECT DATA POINT
		 END DO				!IFRS
	      END DO				!POL.
	   END IF
	END IF
	DO I=0,STHJ(STH_NIFR_J)-1
	   DO I1=0,3
	      LCOR(I1,I)=LCOR(I1,I)+COR(I1,I)
	   END DO
	END DO
C
C WRITE CORRECTIONS
C
 100	CONTINUE
	IF (SCHJ(POFF).EQ.0) SCHJ(POFF)=WNFEOF(FCA) !WHERE TO WRITE
	IF (.NOT.WNFWR(FCA,4*LB_X*STHJ(STH_NIFR_J),LCOR,
	1		SCHJ(POFF))) GOTO 900	!WRITE CORRECTIONS
C
C REWRITE SCAN HEADER
C
 800	CONTINUE
	IF (.NOT.WNFWR(FCA,SCH__L,SCH,SCNP)) GOTO 900 !RE-WRITE SCAN HEADER
C
	RETURN
C
C ERROR
C
 900	CONTINUE
	NSCSWI=.FALSE.
C
	RETURN
C
C
	END

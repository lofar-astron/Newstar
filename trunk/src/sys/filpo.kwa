BEGIN { Output=0; Table=0; Filpo=0; Freq=0; Date=""; Time="00:00:00"; 
        Seqno=0; Last=0; Freqerr=0; Clock=0; Pole=0; 
        Mon="JAN FEB MAR APR MAY JUN JUL AUG SEP OCT NOV DEC"; }

/^----/          { Output=0; }
/^Baseline/      { Filpo=$7; }
/^Frequency/     { Freq=$3; Date=$7; }
/^Freq. point/   { Seqno=$7; Last=$9; }
/^Freq. error/   { Freqerr=$5; }
/^Clock error/   { Clock=$5; }
/^Pole error/    { Pole=$4; }
/^RT/ {

  if (Filpo==Req && Filpo>94) {
    Table=490000+Filpo;

    DD=substr(Date,1,2); YY=substr(Date,6,2);
    for (MM=1; MM<=12 && substr(Mon,4*(MM-1)+1,3)!=substr(Date,3,3); MM++);
    Date=sprintf("%s/%2.2d%/19%s",DD,MM,YY);

    printf("PUT=BASELINEHDR TABLE=%4.4d FILPO=%d OPERATOR=WSRT DATE=%s TIME=%s FREQ=%f FREQERR=%f CLOCK=%f POLE=%f ",Table,Filpo,Date,Time,Freq,Freqerr,Clock,Pole);
    if (Seqno>0 && Last>0) printf("SEQNO=%s%5.5d LAST=%s%5.5d",YY,Seqno,YY,Last);
    printf("\n");
    printf("SELECT=BASELINEHDR TABLE=%4.4d\n",Table);
    Output=1; Filpo=0;
  }
   
}

/^[0-9A-D] / { 
  if (Output) 
     printf("PUT=BASELINE TABLE=%4.4d RT=%s POSITION=%s Q=%s N=%s P=%s\n",Table,$1,$2,$3/10.,$4/10.,$5/10.);
   
}


#  nredo.ssc
# 	WNB 921231
# 
#  Revisions:
#	WNB 930303	NSTAR_DIR added
#	WNB 930305	Make sure aliases
#	WNB 930514	Correct aliases
#	WNB 940124	Leave _TLB
# 
# 	Rebuild Newstar from current files/text library
#
#	Uses environment variables:
#		WNG			where to find wng-type sources
#		WNG_TYPE		machine (sw, dw, hp, al, cv etc)
#		WNG_OLBEXE		root of wng-type .olb, .ppd, .exe
#		WNG_SITE		site (nfra, atnf, rug ...)
#		EXEDWARF_UNIX		where to find/set DWARF .exe, .ppd
#		LIBDWARF		where to find DWARF .olb
#		NSTAR_DIR		N directories
#	and also possible:
#		WNG_EXE, WNG_OLB, WNG_TLB, WNG_ERR, WNG_LIS, WNG_LINK
#
#	Use as:
#		$WNG/nredo.sun c|l|cl [switches] ["dir1 dir2 ..."]	(Unix)
#		@WNG:NREDO c|l|cl [switches] [dir1,dir2,...]		(VAX)
#	c/l compile/link
#	switches e.g. -l1 (always at least - if dir given); -u is default
#	directories e.g. nscan (default all N-directories)
#
# Intro
#
	onintr exex					# finish neatly
	if ($?echo) unset echo				# no verify
	if ($?verbose) unset verbose			# no verbose
	set pid=$$ ; @ dep=1				# for tmp files
	echo " "
	echo "Rebuilding Newstar."
	echo " "
	echo "A log will be made in the standard UPDyymmdd.LOG"
	echo " "
#
# Check environment
#
	if (! $?WNG || ! $?WNG_TYPE || ! $?WNG_OLBEXE ||\
			! $?WNG_SITE || ! $?NSTAR_DIR) then
	  echo " Error: Must have environment variables"
	  echo "        WNG, WNG_TYPE, WNG_OLBEXE, WNG_SITE, NSTAR_DIR defined"
	  echo "        You probably have not included the proper"
	  echo "        files in .cshrc (see README)"
	  goto exex
	endif
	if (! $?EXEDWARF_UNIX || ! $?LIBDWARF) then
	  echo " Error:   Cannot do everything with EXEDWARF_UNIX and/or"
	  echo "          LIBDWARF not defined"
	  echo "          You probably have not included the proper"
	  echo "          files in .cshrc (see README)"
	  goto exex
	endif
        source $WNG/wngcshrc.sun			# get aliases
	if ("`alias nxec`" == "") then
	  echo " Error:   You have no alias for nxec etc."
	  echo "          You probably have not included the proper"
	  echo "          files in .cshrc (see README)"
	  goto exex
	endif
#
# Creating environment
#
	cd $WNG
	if (! $?WNG_EXE) setenv WNG_EXE $WNG_OLBEXE	# make names
	if (! $?WNG_OLB) setenv WNG_OLB $WNG_OLBEXE
	set blddir=($NSTAR_DIR)				# Newstar directories
	if ("$3" != "") set blddir=($3)
	foreach i ($blddir)
	  if (! -d $WNG_EXE/$i) mkdir $WNG_EXE/$i	# make directories
	  if (! -d $WNG_OLB/$i) mkdir $WNG_OLB/$i
	endif
##	if ($?WNG_TLB) unsetenv WNG_TLB			# make sure
	if ($?WNG_ERR) unsetenv WNG_ERR
	if ($?WNG_LIS) unsetenv WNG_LIS
	if ($?WNG_LINK) unsetenv WNG_LINK
#
# Start
#
	echo "Running NREDO.SUN for $WNG_SITE($WNG_TYPE)"
	echo "on `hostname` at `date`"
#
# Compiling
#
GCMP:
	if ("$1" !~ *c* && "$1" !~ *C*) goto LINK	# no compile
	cd $WNG						# base directory
	echo "Compiling ..."
	foreach i ($blddir)				# get .def
	  cd ../$i					# correct directory
	  echo "... $i"
	  ncomp -u $2 ???.grp >& /dev/null		# compile all groups
	end
#
# Link all
#
LINK:
	if ("$1" !~ *l* && "$1" !~ *L*) goto END	# no link
	cd $WNG						# base directory
	echo "Linking Newstar system ..."
	foreach i ($blddir)				# link
	  cd ../$i					# correct directory
	  echo "... $i"
	  nlink -u $2 ???.grp >& /dev/null		# link all
	end
#
# Ready
#
END:
	echo "Newstar rebuilt."
	echo " "
#
#  EXIT
#
exex:
	exit

# nxup.ssc
# WNB 920909
#
# Revisions:
#	WNB 921016	end if typo; directory typo
#	WNB 921113	Preserve space
#	WNB 921122	Delete .udf
#	WNB 921208	Check EXEDWARF
#	WNB 921215	Change to mv
#	WNB 921224	Make SSC
#	WNB 930108	Change order strip, mv
#	HjV 930623	Change cp into mv for saving active executable
#
#	Update in DWARF system. Use as:
#	  $WNG/nxup.sun <type> <dwarflib> <full name> <file name> <logtext>
#			type=1	(copy full name file)
#			    =2	(put file name in OLB)
#			    =3	put file name.ppd in exedwarf
#			    =4	put file name.exe in exedwarf
#			    =5  (put full name in HLB)
#			    =6	(put file name.udf in exedwarf)
#			    =a* delete i.s.o. put
#
#		Uses environment variable EXEDWARF_UNIX
#
# Note: Only .exe, .ppd supported
#
	if (! $?EXEDWARF_UNIX) goto EXIT		# cannot do
	if ($argv[1] =~ *a*) then			# delete
	  if ($argv[1] =~ *4*) then			# type 4
	    if (-e $EXEDWARF_UNIX/$argv[3]:t) then
	      'rm' $EXEDWARF_UNIX/$argv[3]:t		# delete .exe
	    endif
	  endif
	  if ($argv[1] =~ *3*) then			# type 3
	    if (-e $EXEDWARF_UNIX/$argv[3]:t) then
	      'rm' $EXEDWARF_UNIX/$argv[3]:t		# delete .ppd
	    endif
	  endif
	else						# put
	  if ($argv[1] =~ *4*) then			# type 4
	    strip $argv[3]				# strip debug info
	    if (-e $EXEDWARF_UNIX/$argv[3]:t.old) then	# rid oldest
	      'rm' $EXEDWARF_UNIX/$argv[3]:t.old
	    endif
	    if ("$argv[3]:h" != "$EXEDWARF_UNIX") then	# can do
	      if (-e $EXEDWARF_UNIX/$argv[3]:t) then	# remove old
	        'rm' $EXEDWARF_UNIX/$argv[3]:t
	      endif
	      if (-e $EXEDWARF_UNIX/$argv[3]:t) then	# was busy
	        'mv' $EXEDWARF_UNIX/$argv[3]:t $EXEDWARF_UNIX/$argv[3]:t.old
	      endif
	      'mv' $argv[3] $EXEDWARF_UNIX		# save .exe
	    endif
	  endif
	  if ($argv[1] =~ *3*) then			# type 3
	    if ("$argv[3]:h" != "$EXEDWARF_UNIX") then	# can do
	      'mv' $argv[3] $EXEDWARF_UNIX		# save .ppd
	    endif
	  endif
	endif
EXIT:
	exit

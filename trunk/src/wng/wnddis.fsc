C+ WNDDIS.FOR
C  WNB 930510
C
C  Revisions:
C	CMV 931004	First test if local GIPSY setup
C	CMV 931022	Add END clause
C	CMV 931112	For GIDS, change DISP to DEFAULT_DISPLAY
C	AXC 010628	Linux port (READONLY)
C	CMV 030123	Allow other displays than *:0.0
C
	LOGICAL FUNCTION WNDDIS(NGT,DISP)
C
C  Ask X-display to be used
C
C  Result:
C
C	WNDDIS_L = WNDDIS( NGT_L:I)
C				Ask the user for the X-window display to use,
C				and put in DISP.
C				If NGT .true. the GIDS interface will be
C				produced as well.
C
C  Pin references:
C
C	DISPLAY
C
C  Include files:
C
	INCLUDE 'WNG_DEF'
C
C  Parameters:
C
C
C  Arguments:
C
	LOGICAL NGT			!NGIDS SWITCH
	CHARACTER*(*) DISP		!DISPLAY
C
C  Function references:
C
	LOGICAL WNDPAR			!GET USER PARAMETER
	INTEGER WNCALN			!STRING LENGTH
C
C  Data declarations:
C
	CHARACTER*24 HOST		!CURRENT HOST
	CHARACTER*256 LDISP		!LOCAL DISP
	CHARACTER*256 LGIPSY		!GIP_LOC DIRECTORY
	CHARACTER*256 LFILE		!GIPSY TVDEVICES FILE
	CHARACTER*128 LINE		!INPUT LINE
	CHARACTER*128 NAME		!Dummy name
	CHARACTER*128 VAL		!Dummy Char val
	INTEGER LUN			!INPUT/OUTPUT LUN
	INTEGER LL			!LENGTH OF DISP
	LOGICAL FOUND			!FOUND ENTRY IN TVDEVICES
C-
C
C GET DISPLAY
C
	WNDDIS=.TRUE.				!ASSUME OK
	LUN=0					!NO LUN YET
C
	CALL WNGSEG('DISPLAY',LDISP)		!GET POSSIBLE DISPLAY ENVIRONM.
	CALL WNGSGH(HOST)			!GET CURRENT HOST
	IF (LDISP.EQ.' ') LDISP=HOST		!MAKE DEFAULT
	IF (.NOT.WNDPAR('DISPLAY',DISP,LEN(DISP),J0,LDISP)) THEN
	  GOTO 900				!ERROR
	ELSE IF (J0.LE.0) THEN
	  DISP=LDISP				!ASSUME DEFAULT
	ELSE
	  CALL WNCALC(DISP)			!MAKE LC
	END IF
C**	I=INDEX(DISP,':')			!GET RID OF DISPLAY NUMBER
C**	IF (I.GT.0) DISP(I:)=' '		!ASSUME :0.0
C**	IF (DISP.EQ.' ') DISP=HOST
	IF (DISP.EQ.':0.0'.OR.DISP.EQ.':1.0') THEN
	   DISP=HOST//DISP
	END IF
C
C MAKE STRING FOR tvdevices 
C
	LL=WNCALN(DISP)
	I=INDEX(DISP,':')
	LDISP=DISP(1:I-1)//'\:'//DISP(I+1:LL)
	LL=WNCALN(LDISP)
C
C MAKE GIDS ENVIRONMENT
C
	IF (NGT) THEN
C
C TEST IF THIS MACHINE IS DEFINED IN LOCAL GIPSY SETUP
C
	   FOUND=.FALSE.
	   CALL WNGLUN(LUN)                        !GET LUN TO USE
	   IF (LUN.EQ.0) GOTO 903		   !NO FREE LUN
C
C CHECK gids_setup/tvdevices
C
	   IF (.NOT.FOUND) THEN
	      NAME='gids_setup'
	      CALL WNGSEG(NAME,LGIPSY)
	      IF (LGIPSY.NE.' ') THEN
	         LFILE=LGIPSY(1:WNCALN(LGIPSY))//'/tvdevices'
	         OPEN (UNIT=LUN,FILE=LFILE,STATUS='OLD',
#ifdef wn_li__
	1		ERR=901) !OPEN INPUT
#else
	1		READONLY,ERR=901) !OPEN INPUT
#endif
	         DO WHILE (.NOT.FOUND)
	            READ(LUN,'(A)',ERR=901,END=901) LINE
	            FOUND=(LINE(1:LL).EQ.LDISP)
	         END DO
  901	         CONTINUE
	         CLOSE(LUN)
	      END IF
	   END IF
C
C CHECK gip_loc/tvdevices
C
	   IF (.NOT.FOUND) THEN
	      NAME='gip_loc'
	      CALL WNGSEG(NAME,LGIPSY)
	      IF (LGIPSY.NE.' ') THEN
	         LFILE=LGIPSY(1:WNCALN(LGIPSY))//'/tvdevices'
	         OPEN (UNIT=LUN,FILE=LFILE,STATUS='OLD',
#ifdef wn_li__
	1		ERR=902) !OPEN INPUT
#else
	1		READONLY,ERR=902) !OPEN INPUT
#endif
	         DO WHILE (.NOT.FOUND)
	            READ(LUN,'(A)',ERR=902,END=902) LINE
	            FOUND=(LINE(1:LL).EQ.LDISP)
	         END DO
  902	         CONTINUE
	         CLOSE(LUN)
	      END IF
	   END IF
C
C IF NOT DEFINED, MAKE TEMPORARY tvdevices
C
	   IF (.NOT.FOUND) THEN
	     OPEN(UNIT=LUN,FILE='tvdevices',STATUS='UNKNOWN',ERR=903)
	     WRITE(LUN,905) HOST(1:WNCALN(HOST)),LDISP(1:LL)
  905	     FORMAT('# ',A,/,A,':0:$n_exe/gids.exe:153600',/,'# end')
	     CLOSE(LUN)
	     NAME='gids_setup'
	     VAL='.'
	     CALL WNGSES(NAME,VAL)		!LOCATE DEVICE FILE
	   END IF
C
C GIDS uses an interface file. To preserve consistency when the GIDS 
C process runs on another machine, we have to define a full path.
C The name is passed though the environment variable in DISP.
C 
	   DISP='DEFAULT_DISPLAY'
	   CALL WNGSEG(DISP,LGIPSY)
	   IF (LGIPSY.EQ.' ') THEN
	     CALL WNGSEG('HOME',LGIPSY)
	     IF (LGIPSY.NE.' ') THEN
                LGIPSY=LGIPSY(:WNCALN(LGIPSY))//'/.gids_sockets.1'
	     ELSE
	        LGIPSY='gids_sockets.1'
	     END IF
	     CALL WNGSES(DISP(:WNCALN(DISP)),LGIPSY(:WNCALN(LGIPSY)))
	   END IF
C
	ELSE
C**	  DISP=DISP(:WNCALN(DISP))//':0.0'	!FINAL DISPLAY NAME
	END IF
	GOTO 800
C
C ERROR
C
 903	CONTINUE
	CALL WNCTXT(F_TP,'Cannot open tvdevices file')
 900	CONTINUE
	WNDDIS=.FALSE.				!ERROR
	DISP=' '
C
C FINISH
C
 800	CONTINUE
	IF (LUN.NE.0) CALL WNGLUF(LUN)		!FREE LUN
C
	RETURN
C
C
	END





